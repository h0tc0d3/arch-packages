pkgname=qt5-declarative+clang
pkgver=5.15.4+kde+r17
pkgrel=1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QML and JavaScript languages'
provides=("qt5-declarative" "qt5-declarative=${pkgver}")
depends=('qt5-base')
makedepends=('python' 'vulkan-headers' 'git')
groups=('qt' 'qt5')
conflicts=('qtchooser' 'qt5-declarative')
_commit=51efb2ed2f071beda188270a23ac450fe4b318f7
source=(
  "git+https://invent.kde.org/qt/qt/qtdeclarative#commit=${_commit}"
)
sha256sums=('SKIP')

_llvm=0

pkgver() {

  cd "${srcdir:?}/qtdeclarative" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/qtdeclarative directory! PkgVer Failed! \E[0m"
    exit 1
  )

  echo "5.15.4+kde+r$(git rev-list --count v5.15.4-lts-lgpl..${_commit})"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  if [[ ! -d build ]]; then
    mkdir -p build
  fi

}

build() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  qmake ../qtdeclarative
  make

}

package() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Package Failed! \E[0m"
    exit 1
  )

  make INSTALL_ROOT="${pkgdir:?}" install

  # Symlinks for backwards compatibility
  for b in "${pkgdir:?}"/usr/bin/*; do
    ln -s "${b##*/}" "${pkgdir:?}/usr/bin/${b##*/}-qt5"
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir:?}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt5-base "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
