pkgname=libarchive+clang
pkgver=3.6.1
pkgrel=2
pkgdesc='Multi-format archive and compression library'
arch=('x86_64')
url='https://libarchive.org/'
license=('BSD')
depends=('acl' 'libacl.so' 'bzip2' 'expat' 'lz4' 'openssl' 'xz' 'zlib' 'zstd')
provides=('libarchive.so' libarchive "libarchive=${pkgver}")
conflicts=(libarchive)
validpgpkeys=('A5A45B12AD92D964B89EEE2DEC560C81CEC2276E') # Martin Matuska <mm@FreeBSD.org>
source=(
  "https://github.com/libarchive/libarchive/releases/download/v${pkgver}/libarchive-${pkgver}.tar.xz"{,.asc}
  '0001-libarchive-Handle-a-calloc-returning-NULL-fixes-1754.patch'
  '0002-Validate-entry_bytes_remaining-in-pax_attribute.patch'
  '0003-fix-build-with-glibc-2.36.patch'
)
sha256sums=('5a411aceb978f43e626f0c2d1812ddd8807b645ed892453acabd532376c148e6'
  'SKIP'
  'bc52b2b2b99915894b436c97872d5d50e94c8c7483865a028fad9a710c837fa7'
  '38c8d9b00f3259558e67e6fdf790ccbf8ecbba2de101476c2416d87b1679bcb9'
  '01d2c724ac7f1b678b81b9204c4c913abbf44e6f00b343ced0da8b325fe609c8')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d "libarchive-${pkgver}" -Np1 -i "${srcdir:?}/0001-libarchive-Handle-a-calloc-returning-NULL-fixes-1754.patch"
  patch -d "libarchive-${pkgver}" -Np1 -i "${srcdir:?}/0002-Validate-entry_bytes_remaining-in-pax_attribute.patch"
  patch -d "libarchive-${pkgver}" -Np1 -i "${srcdir:?}/0003-fix-build-with-glibc-2.36.patch"

}

build() {

  cd "${srcdir:?}/libarchive-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libarchive-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --without-xml2 \
    --without-nettle \
    --disable-static

  make

}

check() {

  LC_ALL=C make check -C "libarchive-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "libarchive-${pkgver}"
  install -Dm0644 "libarchive-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/libarchive/COPYING"

}
