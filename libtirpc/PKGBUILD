pkgname=libtirpc+clang
pkgver=1.3.3
pkgrel=1
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('x86_64')
url="http://git.linux-nfs.org/?p=steved/libtirpc.git;a=summary"
license=('BSD')
depends=('krb5')
provides=('libtirpc' "libtirpc=${pkgver}")
conflicts=('libtirpc')
backup=('etc/netconfig')
# git tree: git://linux-nfs.org/~steved/libtirpc
source=("https://downloads.sourceforge.net/sourceforge/libtirpc/libtirpc-${pkgver}.tar.bz2")
sha256sums=('6474e98851d9f6f33871957ddee9714fdcd9d8a5ee9abb5a98d63ea2e60e12f3')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libtirpc-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libtirpc-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconf=/etc

  make

}

package() {

  cd "${srcdir:?}/libtirpc-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libtirpc-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -D -m644 COPYING "${pkgdir:?}"/usr/share/licenses/${pkgname}/LICENSE

}
