pkgname=openssh+clang
pkgver=8.9p1
pkgrel=1
pkgdesc='Premier connectivity tool for remote login with the SSH protocol'
url='https://www.openssh.com/portable.html'
license=('custom:BSD')
arch=('x86_64')
provides=('openssh' "openssh=${pkgver}")
conflicts=('openssh')
depends=('glibc' 'krb5' 'openssl' 'libedit' 'ldns' 'libxcrypt' 'libcrypt.so' 'zlib' 'pam' 'libselinux')
makedepends=('linux-headers' 'libfido2')
optdepends=(
  'xorg-xauth: X11 forwarding'
  'x11-ssh-askpass: input passphrase in X'
  'libfido2: FIDO/U2F support'
)
validpgpkeys=('7168B983815A5EEF59A4ADFD2A3F414E736060BA')
#source=("git://anongit.mindrot.org/openssh.git?signed#tag=V_8_2_P1"
source=(
  "https://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/openssh-${pkgver}.tar.gz"{,.asc}
  'sshdgenkeys.service'
  'sshd.service'
  'sshd.conf'
  'sshd.pam'
)
sha256sums=('fd497654b7ab1686dac672fb83dfb4ba4096e8b5ffcdaccd262380ae58bec5e7'
  'SKIP'
  '4031577db6416fcbaacf8a26a024ecd3939e5c10fe6a86ee3f0eea5093d533b7'
  'e40f8b7c8e5e2ecf3084b3511a6c36d5b5c9f9e61f2bb13e3726c71dc7d4fbc7'
  '4effac1186cc62617f44385415103021f72f674f8b8e26447fc1139c670090f6'
  '64576021515c0a98b0aaf0a0ae02e0f5ebe8ee525b1e647ab68f369f81ecd846')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd')

install=openssh.install

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/openssh-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/openssh-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  autoreconf -fiv

}

build() {

  cd "${srcdir:?}/openssh-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/openssh-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh \
    --disable-strip \
    --with-ldns \
    --with-libedit \
    --with-security-key-builtin \
    --with-ssl-engine \
    --with-pam \
    --with-selinux \
    --with-privsep-user=nobody \
    --with-kerberos5=/usr \
    --with-xauth=/usr/bin/xauth \
    --with-md5-passwords \
    --with-pid-dir=/run \
    --with-default-path='/usr/local/sbin:/usr/local/bin:/usr/bin'

  make

}

check() {

  cd "${srcdir:?}/openssh-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/openssh-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  # Tests require openssh to be already installed system-wide,
  # also connectivity tests will fail under makechrootpkg since
  # it runs as nobody which has /bin/false as login shell.
  if [[ -e /usr/bin/scp && ! -e /.arch-chroot ]]; then
    make tests
  fi

}

package() {

  cd "${srcdir:?}/openssh-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/openssh-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  ln -sf ssh.1.gz "${pkgdir:?}/usr/share/man/man1/slogin.1.gz"
  install -Dm644 LICENCE "${pkgdir:?}/usr/share/licenses/openssh/LICENCE"

  install -Dm644 ../sshdgenkeys.service "${pkgdir:?}/usr/lib/systemd/system/sshdgenkeys.service"
  install -Dm644 ../sshd.service "${pkgdir:?}/usr/lib/systemd/system/sshd.service"
  install -Dm644 ../sshd.conf "${pkgdir:?}/usr/lib/tmpfiles.d/sshd.conf"
  install -Dm644 ../sshd.pam "${pkgdir:?}/etc/pam.d/sshd"

  install -Dm755 contrib/findssl.sh "${pkgdir:?}/usr/bin/findssl.sh"
  install -Dm755 contrib/ssh-copy-id "${pkgdir:?}/usr/bin/ssh-copy-id"
  install -Dm644 contrib/ssh-copy-id.1 "${pkgdir:?}/usr/share/man/man1/ssh-copy-id.1"

  sed \
    -e '/^#KbdInteractiveAuthentication yes$/c KbdInteractiveAuthentication no' \
    -e '/^#PrintMotd yes$/c PrintMotd no # pam does that' \
    -e '/^#UsePAM no$/c UsePAM yes' \
    -i "${pkgdir:?}/etc/ssh/sshd_config"

}
