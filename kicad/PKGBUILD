pkgname=kicad+clang
pkgver=6.0.7
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('x86_64')
url="http://kicad-pcb.org/"
license=('GPL')
depends=('wxwidgets-gtk3' 'python-wxpython' 'python' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice' 'opencascade')
makedepends=('git' 'cmake' 'mesa' 'boost' 'swig')
optdepends=(
  'kicad-library: for footprints, symbols and templates'
  'kicad-library-3d: for 3D models of components'
)
conflicts=('kicad' 'kicad-bzr')
provides=('kicad')
install=kicad.install
_commit=f9a2dced07acac97c62eef0931269fea6bcfb828
source=(
  "kicad::git+https://gitlab.com/kicad/code/kicad.git#commit=${_commit}"
  fix-archive-permissions.patch
  fix-version-string.patch
)
sha256sums=('SKIP'
  '282dfe161a2ac23885cd1265364d1276725196c211cdeeb2c3b96163a09888d4'
  'a22dc2900a51d354f2917cd1ad15e077cd0932db3029107641974d88c52a8bf9')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  if [ -d build ]; then
    rm -rf build
  fi

  patch -d kicad -p1 <fix-archive-permissions.patch
  patch -d kicad -p1 <fix-version-string.patch

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -S kicad \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKICAD_USE_OCC=ON \
    -DKICAD_SCRIPTING_WXPYTHON=ON \
    -DKICAD_USE_EGL=ON \
    -DKICAD_BUILD_I18N=ON \
    -DKICAD_I18N_UNIX_STRICT_PATH=ON \
    -DKICAD_BUILD_QA_TESTS=OFF \
    -Wno-dev

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
