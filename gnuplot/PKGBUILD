pkgbase=gnuplot+clang
pkgname=(gnuplot+clang gnuplot-demos+clang)
pkgver=5.4.4
pkgrel=1
pkgdesc='Plotting package which outputs to X11, PostScript, PNG, GIF, and others'
arch=(x86_64)
url='http://www.gnuplot.info'
license=(custom)
depends=(gd wxwidgets-gtk3 lua qt5-svg libcerf libcaca)
makedepends=(texlive-core emacs texlive-latexextra qt5-tools)
source=(
  "https://downloads.sourceforge.net/sourceforge/gnuplot/gnuplot-${pkgver}.tar.gz"
  lua53_compat.patch
)
sha256sums=('372300b7867f5b3538b25fc5d0ac7734af6e3fe0d202b6db926e4369913f0902'
  'bfd8a61abbf4491c74225cb9fd252619d4fc29751838bcb4c0639ffe05a00695')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/gnuplot-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/gnuplot-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # fix default source location; use the GDFONTPATH variable to modify at runtime
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c

  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
    -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
    src/variable.c

  patch -p1 <"${srcdir:?}/lua53_compat.patch"

}

build() {

  cd "${srcdir:?}/gnuplot-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/gnuplot-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  MAKEINFO=/usr/bin/makeinfo \
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/bin \
    --with-gihdir=/usr/share/gnuplot \
    --with-readline=gnu \
    --with-bitmap-terminals \
    --with-wx-single-threaded \
    --with-caca \
    --with-texdir=/usr/share/texmf/tex/latex/gnuplot

  make pkglibexecdir=/usr/bin

}

package_gnuplot+clang() {

  provides=('gnuplot' "gnuplot=${pkgver}")
  conflicts=('gnuplot')

  make pkglibexecdir=/usr/bin DESTDIR="${pkgdir:?}" install -C "gnuplot-${pkgver}"
  make DESTDIR="${pkgdir:?}" install -C "gnuplot-${pkgver}/demo"

  install -Dm644 "gnuplot-${pkgver}/Copyright" -t "${pkgdir:?}/usr/share/licenses/gnuplot"

  rm -f "${pkgdir:?}/usr/share/texmf-dist/ls-R"

}

package_gnuplot-demos+clang() {

  pkgdesc='Demos for gnuplot'
  depends=(gnuplot)
  provides=('gnuplot-demos' "gnuplot-demos=${pkgver}")
  conflicts=('gnuplot-demos')

  install -Dm644 "gnuplot-${pkgver}/demo/"*.{dem,dat} -t "${pkgdir:?}/usr/share/gnuplot/demos"

}
