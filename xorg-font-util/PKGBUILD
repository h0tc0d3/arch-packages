pkgname=xorg-font-util+clang
pkgver=1.3.3
pkgrel=1
pkgdesc='X.Org font utilities'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
provides=('font-util' "xorg-font-util" "xorg-font-util=${pkgver}")
conflicts=('font-util' "xorg-font-util")
makedepends=('xorg-util-macros')
source=("https://xorg.freedesktop.org/releases/individual/font/font-util-${pkgver}.tar.xz"{,.sig})
sha256sums=('e791c890779c40056ab63aaed5e031bb6e2890a98418ca09c534e6261a2eebd2'
  'SKIP')
groups=('xorg-fonts' 'xorg')
validpgpkeys=(
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
)
_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi
}

build() {

  cd "${srcdir:?}/font-util-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/font-util-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-mapdir=/usr/share/fonts/util \
    --with-fontrootdir=/usr/share/fonts

  make

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "font-util-${pkgver}"
  install -Dm644 "font-util-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/${pkgname}/COPYING"

}
