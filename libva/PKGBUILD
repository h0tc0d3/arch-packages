pkgname=libva+clang
pkgver=2.15.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=(x86_64)
url=https://01.org/linuxmedia/vaapi
license=(MIT)
provides=(libva "libva=${pkgver}" libva-drm.so libva-glx.so libva-wayland.so libva-x11.so libva.so)
conflicts=(libva)
depends=(libdrm libgl libx11 libxext libxfixes wayland)
makedepends=(git libglvnd mesa meson)
optdepends=(
  'intel-media-driver: backend for Intel GPUs (>= Broadwell)'
  'libva-vdpau-driver: backend for Nvidia and AMD GPUs'
  'libva-intel-driver: backend for Intel GPUs (<= Haswell)'
)
backup=(etc/libva.conf)
_commit=b095d10bf355110352e75c22e581018a7ea7de5a
source=("git+https://github.com/intel/libva.git#commit=${_commit}")
sha256sums=(SKIP)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

pkgver() {

  cd "${srcdir:?}/libva" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libva directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags

}

build() {

  CFLAGS+=" -DENABLE_VA_MESSAGING" # Option missing

  meson setup libva build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  ninja -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm 644 libva/COPYING -t "${pkgdir:?}/usr/share/licenses/libva"

  install -Dm 644 /dev/stdin "${pkgdir:?}/etc/libva.conf" <<END
LIBVA_MESSAGING_LEVEL=1
END

}
