pkgname=libx11+clang
pkgver=1.8.1
pkgrel=2
pkgdesc="X11 client-side library"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
provides=("libx11" "libx11=${pkgver}")
conflicts=("libx11")
# keep xorgproto runtime dependency
# https://lists.archlinux.org/pipermail/arch-dev-public/2019-December/029767.html
depends=('libxcb' 'xorgproto')
makedepends=('xorg-util-macros' 'xtrans')
license=('custom')
source=(
  "https://xorg.freedesktop.org/releases/individual/lib/libX11-${pkgver}.tar.xz"{,.sig}
  dont-forward-keycode-0.patch
)
sha256sums=('1bc41aa1bbe01401f330d76dfa19f386b79c51881c7bbfee9eb4e27f22f2d9f7'
  'SKIP'
  '47ae492f52933334c0d3a16dc0cda17736a16b326556bafd49e545933a729ac3')
validpgpkeys=(
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alanc@freedesktop.org>
  #'C41C985FDCF1E5364576638B687393EE37D128F8' # Matthieu Herrb <matthieu.herrb@laas.fr>
  #'3BB639E56F861FA2E86505690FDD682D974CA72A' # Matt Turner <mattst88@gmail.com>
  #'995ED5C8A6138EB0961F18474C09DD83CAAA50B2' # Adam Jackson <ajax@nwnk.net>
  #'C383B778255613DFDB409D91DB221A6900000011' # "Keith Packard <keithp@keithp.com>"
)
_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libX11-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libX11-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  patch -Np1 -i "${srcdir:?}/dont-forward-keycode-0.patch"
  autoreconf -fiv

}

build() {

  cd "${srcdir:?}/libX11-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libX11-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-silent-rules

  make

}

check() {

  cd "${srcdir:?}/libX11-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libX11-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/libX11-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libX11-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -d -m755 "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
