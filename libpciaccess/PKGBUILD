pkgname=libpciaccess+clang
pkgver=0.17
pkgrel=1
pkgdesc="X11 PCI access library"
arch=(x86_64)
license=('custom')
url="https://xorg.freedesktop.org/"
provides=(libpciaccess "libpciaccess=${pkgver}")
conflicts=(libpciaccess)
depends=('glibc')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libpciaccess-${pkgver}.tar.xz"{,.sig})
sha512sums=('8484605c66ef18c8d8a3e029a6d33e26fcaa450c1263790d89ac8b0a160ffb2dfceaa6140ac9ad7d8087283ecbec8ac801f757e44890d547c3cbecd2c922ca47'
  'SKIP')
validpgpkeys=(
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
  '8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D' # "Emil Velikov <emil.l.velikov@gmail.com>"
  '995ED5C8A6138EB0961F18474C09DD83CAAA50B2' # "Adam Jackson <ajax@nwnk.net>"
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libpciaccess-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libpciaccess-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make

}

package() {

  cd "${srcdir:?}/libpciaccess-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libpciaccess-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
