pkgname=libpciaccess-noname
pkgver=0.16
pkgrel=2
pkgdesc="X11 PCI access library"
arch=(x86_64)
license=('custom')
url="https://xorg.freedesktop.org/"
provides=(libpciaccess "libpciaccess=${pkgver}")
conflicts=(libpciaccess)
depends=('glibc')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libpciaccess-${pkgver}.tar.bz2"{,.sig})
sha512sums=('2f250048a270dfc0823d4bdd613aa272c58a80eaafd922850f56c4b6f7a45a263ed4cf521b52b49f04484ea44ebefb7407d079aa058318a5751ffb181e38fed1'
  'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')  # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys+=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # "Emil Velikov <emil.l.velikov@gmail.com>"
validpgpkeys+=('995ED5C8A6138EB0961F18474C09DD83CAAA50B2') # "Adam Jackson <ajax@nwnk.net>"

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libpciaccess-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libpciaccess-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" \
      ./configure \
      --prefix=/usr \
      --sysconfdir=/etc

    CFLAGS="${CFLAGS} -flto=thin" CXXFLAGS="${CXXFLAGS} -flto=thin" make

  else

    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc

    make

  fi

}

package() {

  cd "${srcdir:?}/libpciaccess-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libpciaccess-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
