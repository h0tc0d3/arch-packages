pkgname=vulkan-icd-loader+clang
pkgver=1.3.217
pkgrel=1
arch=(x86_64)
pkgdesc="Vulkan Installable Client Driver (ICD) Loader"
url="https://www.khronos.org/vulkan/"
license=('custom')
provides=('libvulkan.so' 'vulkan-icd-loader' "vulkan-icd-loader=${pkgver}")
conflicts=('vulkan-icd-loader')
makedepends=('cmake' 'python-lxml' 'libx11' 'libxrandr' 'wayland' 'vulkan-headers')
depends=(glibc)
optdepends=('vulkan-driver: packaged vulkan driver') # vulkan-driver: vulkan-intel/vulkan-radeon/nvidia-utils/....
source=("vulkan-icd-loader-${pkgver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Loader/archive/v${pkgver}.tar.gz")
sha256sums=('59c0e4934fca328366bbb50b3a8d2dfc3daeab7589fdadd7a29b8a79fe242a7f')

_llvm=0

prepare() {

	if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
		_llvm=1
	fi

}

build() {

	if [[ ${_llvm} -eq 1 ]]; then

		CFLAGS="${CFLAGS} -flto=thin"
		CXXFLAGS="${CXXFLAGS} -flto=thin"
		LDFLAGS="${LDFLAGS} -fuse-ld=lld"

	fi

	cmake -B build -S Vulkan-Loader-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DVULKAN_HEADERS_INSTALL_DIR=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_INSTALL_SYSCONFDIR=/etc \
		-DCMAKE_INSTALL_DATADIR=/share \
		-DCMAKE_SKIP_RPATH=True \
		-DBUILD_TESTS=Off \
		-DBUILD_WSI_XCB_SUPPORT=On \
		-DBUILD_WSI_XLIB_SUPPORT=On \
		-DBUILD_WSI_WAYLAND_SUPPORT=On \
		-DCMAKE_BUILD_TYPE=Release

	cmake --build build

}

package() {

	DESTDIR="${pkgdir:?}" cmake --install build

	install -dm755 "${pkgdir:?}/usr/share/licenses/${pkgname}"
	install -m644 "Vulkan-Loader-${pkgver}/LICENSE.txt" "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
