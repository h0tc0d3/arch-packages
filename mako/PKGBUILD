pkgname=mako+clang
pkgver=1.6
pkgrel=2
license=('MIT')
provides=('mako' "mako=${pkgver}")
conflicts=('mako')
pkgdesc='Lightweight notification daemon for Wayland'
makedepends=("meson" "scdoc" "systemd" "wayland-protocols")
depends=(
  "gdk-pixbuf2"
  "pango"
  "systemd-libs"
  "wayland"
)
optdepends=("jq: support for 'makoctl menu'")
arch=("x86_64")
url='https://mako-project.org'
source=(
  "mako-${pkgver}.tar.gz::https://github.com/emersion/mako/releases/download/v${pkgver}/mako-${pkgver}.tar.gz"
  "mako-${pkgver}.tar.gz.sig::https://github.com/emersion/mako/releases/download/v${pkgver}/mako-${pkgver}.tar.gz.sig"
  "systemd-dbus-activation.patch"
)
validpgpkeys=("34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48") # emersion
sha256sums=('9f43cba9e1b43c69be8c9e4a79c358b3cd22153ca3ffb8bf0ee7aa30c59b2fc2'
  'SKIP'
  '4579a2673dcf2114779e10ed52d771f3930c2192e8e35d3e145163d9e0b45c20')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  patch -Np1 -i "${srcdir:?}/systemd-dbus-activation.patch" -d "mako-${pkgver}"

}

build() {

  meson setup "mako-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D sd-bus-provider=libsystemd

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm644 -t "${pkgdir:?}/usr/share/licenses/mako/" "mako-${pkgver}/LICENSE"
  install -Dm644 -t "${pkgdir:?}/usr/lib/systemd/user/" "mako-${pkgver}/contrib/systemd/mako.service"

}
