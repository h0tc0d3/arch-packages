pkgname=spirv-headers+clang
epoch=1
pkgver=1.3.216.0
pkgrel=1
pkgdesc="SPIR-V Headers"
arch=(any)
url="https://www.khronos.org/registry/spir-v/"
license=(MIT)
provides=('spirv-headers' "spirv-headers=${pkgver}")
conflicts=('spirv-headers')
makedepends=(cmake git)
_commit=b2a156e1c0434bc8c99aaebba1c7be98be7ac580
source=("git+https://github.com/KhronosGroup/SPIRV-Headers.git#commit=${_commit}")
sha256sums=('SKIP')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build \
    -S SPIRV-Headers \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -Dm644 "SPIRV-Headers/LICENSE" -t "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
