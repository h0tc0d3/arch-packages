pkgbase=linux-tools+clang
pkgname=(
  'bpf+clang'
  'cgroup_event_listener+clang'
  'cpupower+clang'
  'linux-tools-meta+clang'
  'perf+clang'
  #  'python-perf+clang'
  'tmon+clang'
  'turbostat+clang'
  'usbip+clang'
  'x86_energy_perf_policy+clang'
)
pkgver=5.14.11
pkgrel=1
license=('GPL2')
arch=('x86_64')
url='https://www.kernel.org'
makedepends=('git')
# split packages need all package dependencies set manually in makedepends
# kernel source deps
makedepends+=('asciidoc' 'xmlto')
# perf deps
makedepends+=('perl' 'python' 'slang' 'elfutils' 'libunwind' 'numactl' 'audit' 'zstd' 'libcap')
# cpupower deps
makedepends+=('pciutils')
# usbip deps
makedepends+=('glib2' 'sysfsutils' 'udev')
# tmon deps
makedepends+=('ncurses')
# bpf deps
makedepends+=('readline' 'zlib' 'libelf' 'libcap' 'python-docutils')
# turbostat deps
makedepends+=('libcap')
groups=("${pkgbase}")
source=(
  "https://cdn.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${pkgver}.tar"{.xz,.sign}
  'cpupower.default'
  'cpupower.systemd'
  'cpupower.service'
  'usbipd.service'
  0001-tools-perf-Use-long-running-addr2line-per-dso.patch
)
validpgpkeys=(
  'ABAF11C65A2970B130ABE3C479BE3E4300411886' # Linus Torvalds
  '647F28654894E3BD457199BE38DBBDC86092693E' # Greg Kroah-Hartman
)
sha256sums=('93830a24df5342fb8fb857b7ca58d637f99d84b0ac54610fbf49743e3e8d5731'
  'SKIP'
  '4fa509949d6863d001075fa3e8671eff2599c046d20c98bb4a70778595cd1c3f'
  'b692f4859ed3fd9831a058a450a84d8c409bf7e3e45aac1c2896a14bb83f3d7a'
  '42d2ec9f1d9cc255ee7945a27301478364ef482f5a6ddfc960189f03725ccec2'
  '2e187734d8aec58a3046d79883510d779aa93fb3ab20bd3132c1a607ebe5498f'
  'c75a7ed4198dab166f1f6b1511515dbd69ef858917f3f914da06813dccc9666a')

_llvm=0

prepare() {

  cd linux-${pkgver}

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1

    sed -i -e 's/gcc/clang/g;s/$(CROSS)ar/llvm-ar/;s/$(CROSS)strip/llvm-strip/;s/$(CROSS)ranlib/llvm-ranlib/' \
      tools/power/cpupower/Makefile

    sed -i -e 's/gcc/clang/' \
      tools/power/x86/x86_energy_perf_policy/Makefile

    sed -i -e 's/gcc/clang/' \
      tools/power/x86/turbostat/Makefile

  fi

  patch -p1 -N -i "${srcdir:?}/0001-tools-perf-Use-long-running-addr2line-per-dso.patch"

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  echo ':: perf'
  pushd linux-${pkgver}/tools/perf
  make -f Makefile.perf \
    prefix=/usr \
    lib=lib/perf \
    perfexecdir=lib/perf \
    NO_SDT=1 \
    PYTHON=python \
    PYTHON_CONFIG=python-config \
    PERF_VERSION=${pkgver}-${pkgrel} \
    DESTDIR="${pkgdir:?}"
  popd

  echo ':: cpupower'
  pushd linux-${pkgver}/tools/power/cpupower
  make VERSION=${pkgver}-${pkgrel}
  popd

  echo ':: x86_energy_perf_policy'
  pushd linux-${pkgver}/tools/power/x86/x86_energy_perf_policy
  make
  popd

  echo ':: usbip'
  pushd linux-${pkgver}/tools/usb/usbip
  # Fix gcc compilation
  sed -i 's,-Wall -Werror -Wextra,-fcommon,' configure.ac
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  make
  popd

  echo ':: tmon'
  pushd linux-${pkgver}/tools/thermal/tmon
  make
  popd

  echo ':: cgroup_event_listener'
  pushd linux-${pkgver}/tools/cgroup
  make
  popd

  echo ':: turbostat'
  pushd linux-${pkgver}/tools/power/x86/turbostat
  make
  popd

  echo ':: bpf'
  pushd linux-${pkgver}/tools/bpf
  # doesn't compile when we don't first compile bpftool in its own directory and
  # man pages require to be also launch from the subdirectory
  make -C bpftool all doc
  # runqslower, require kernel binary path to build, skip it
  make -W runqslower
  popd

}

package_linux-tools-meta+clang() {
  pkgdesc='Linux kernel tools meta package'
  groups=()
  depends=(
    'bpf+clang'
    'cgroup_event_listener+clang'
    'cpupower+clang'
    'linux-tools-meta+clang'
    'perf+clang'
    #  'python-perf+clang'
    'tmon+clang'
    'turbostat+clang'
    'usbip+clang'
    'x86_energy_perf_policy+clang'
  )
  conflicts=(
    'acpidump'
  )
}

package_perf+clang() {

  pkgdesc='Linux kernel performance auditing tool'
  depends=('glibc' 'perl' 'python' 'slang' 'elfutils' 'libunwind' 'binutils'
    'numactl' 'audit' 'coreutils' 'glib2' 'xz' 'zlib' 'libelf' 'bash'
    'zstd' 'libcap')
  provides=("perf" "perf=${pkgver}")
  conflicts=("perf")

  cd linux-${pkgver}/tools/perf
  make -f Makefile.perf \
    prefix=/usr \
    lib=lib/perf \
    perfexecdir=lib/perf \
    NO_SDT=1 \
    PYTHON=python \
    PYTHON_CONFIG=python-config \
    PERF_VERSION=${pkgver}-${pkgrel} \
    DESTDIR="${pkgdir:?}" \
    install
  cd "${pkgdir:?}"
  # add linker search path
  mkdir "${pkgdir:?}/etc/ld.so.conf.d"
  echo '/usr/lib/perf' >"${pkgdir:?}/etc/ld.so.conf.d/${pkgname:?}.conf"
  # move completion in new directory
  install -Dm644 etc/bash_completion.d/perf usr/share/bash-completion/completions/perf
  rm -r etc/bash_completion.d
  # no exec on usr/share
  find usr/share -type f -exec chmod a-x {} \;

}

#package_python-perf(){
#  pkgdesc='Linux kernel performance auditing tool (python bindings)'
#  depends=('python')
#
#  cd linux-${pkgver}/tools/perf
#
#  make install-python_ext PYTHON=python DESTDIR="${pkgdir:?}"
#}

package_cpupower+clang() {

  pkgdesc='Linux kernel tool to examine and tune power saving related features of your processor'
  backup=('etc/default/cpupower')
  depends=('glibc' 'bash' 'pciutils')
  conflicts=('cpufrequtils' 'cpupower')
  provides=("cpupower" "cpupower=${pkgver}")
  replaces=('cpufrequtils')
  install=cpupower.install

  pushd linux-${pkgver}/tools/power/cpupower
  make \
    DESTDIR="${pkgdir:?}" \
    sbindir='/usr/bin' \
    libdir='/usr/lib' \
    mandir='/usr/share/man' \
    docdir='/usr/share/doc/cpupower' \
    install install-man
  popd
  # install startup scripts
  install -Dm 644 cpupower.default "${pkgdir:?}/etc/default/cpupower"
  install -Dm 644 cpupower.service "${pkgdir:?}/usr/lib/systemd/system/cpupower.service"
  install -Dm 755 cpupower.systemd "${pkgdir:?}/usr/lib/systemd/scripts/cpupower"

}

package_x86_energy_perf_policy+clang() {

  pkgdesc='Read or write MSR_IA32_ENERGY_PERF_BIAS'
  depends=('glibc')
  provides=("x86_energy_perf_policy" "x86_energy_perf_policy=${pkgver}")
  conflicts=("x86_energy_perf_policy")

  cd linux-${pkgver}/tools/power/x86/x86_energy_perf_policy
  install -Dm 755 x86_energy_perf_policy "${pkgdir:?}/usr/bin/x86_energy_perf_policy"
  install -Dm 644 x86_energy_perf_policy.8 "${pkgdir:?}/usr/share/man/man8/x86_energy_perf_policy.8"

}

package_usbip+clang() {

  pkgdesc='An USB device sharing system over IP network'
  depends=('glibc' 'glib2' 'sysfsutils' 'systemd-libs')
  provides=("usbip" "usbip=${pkgver}")
  conflicts=("usbip")

  pushd linux-${pkgver}/tools/usb/usbip
  make install DESTDIR="${pkgdir:?}"
  popd
  # module loading
  install -Dm 644 /dev/null "${pkgdir:?}/usr/lib/modules-load.d/${pkgname:?}.conf"
  printf 'usbip-core\nusbip-host\n' >"${pkgdir:?}/usr/lib/modules-load.d/${pkgname:?}.conf"
  # systemd
  install -Dm 644 usbipd.service "${pkgdir:?}/usr/lib/systemd/system/usbipd.service"

}

package_tmon+clang() {

  pkgdesc='Monitoring and Testing Tool for Linux kernel thermal subsystem'
  depends=('glibc' 'ncurses')
  provides=("tmonf" "tmon=${pkgver}")
  conflicts=("tmon")

  cd linux-${pkgver}/tools/thermal/tmon
  make install INSTALL_ROOT="${pkgdir:?}"

}

package_cgroup_event_listener+clang() {

  pkgdesc='Simple listener of cgroup events'
  depends=('glibc')
  provides=("cgroup_event_listener" "cgroup_event_listener=${pkgver}")
  conflicts=("cgroup_event_listener")

  cd linux-${pkgver}/tools/cgroup
  install -Dm755 cgroup_event_listener "${pkgdir:?}/usr/bin/cgroup_event_listener"

}

package_turbostat+clang() {

  pkgdesc='Report processor frequency and idle statistics'
  depends=('glibc' 'libcap')
  provides=("turbostat" "turbostat=${pkgver}")
  conflicts=("turbostat")

  cd linux-${pkgver}/tools/power/x86/turbostat
  make install DESTDIR="${pkgdir:?}"

}

package_bpf+clang() {

  pkgdesc='BPF tools'
  depends=('glibc' 'readline' 'zlib' 'libelf' 'libcap')
  provides=("bpf" "bpf=${pkgver}")
  conflicts=("bpf")

  cd linux-${pkgver}/tools/bpf
  # skip runsqlower until disabled in build
  make -W runqslower_install install prefix=/usr DESTDIR="${pkgdir:?}"
  # fix bpftool hard written path
  mv "${pkgdir:?}"/usr/sbin/bpftool "${pkgdir:?}"/usr/bin/bpftool
  rmdir "${pkgdir:?}"/usr/sbin
  # install man pages
  make -C bpftool doc-install prefix=/usr/share DESTDIR="${pkgdir:?}"

}

# vim:set ts=2 sw=2 et:
