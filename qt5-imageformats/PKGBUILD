pkgname=qt5-imageformats+clang
pkgver=5.15.6+kde+r4
pkgrel=1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
provides=("qt5-imageformats" "qt5-imageformats=${pkgver}")
conflicts=("qt5-imageformats")
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=('qt5-base' 'jasper' 'libmng' 'libwebp')
makedepends=('git')
groups=('qt' 'qt5')
_commit=9044695f4b1acdd7ec24e7b01f6a6ca61f982a75
source=("git+https://invent.kde.org/qt/qt/qtimageformats#commit=${_commit}")
sha256sums=('SKIP')

_llvm=0

pkgver() {

  cd "${srcdir:?}/qtimageformats" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/qtimageformats directory! PkgVer Failed! \E[0m"
    exit 1
  )

  echo "5.15.6+kde+r$(git rev-list --count v5.15.6-lts-lgpl..${_commit})"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  if [[ ! -d build ]]; then
    mkdir -p build
  fi

}

build() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  qmake ../qtimageformats
  make

}

package() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Package Failed! \E[0m"
    exit 1
  )

  make INSTALL_ROOT="${pkgdir:?}" install

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt5-base "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
