pkgname=libxau+clang
pkgver=1.0.10
pkgrel=1
pkgdesc="X11 authorisation library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorgproto')
license=('custom')
provides=("libxau" "libxau=${pkgver}")
conflicts=("libxau")
source=("https://xorg.freedesktop.org/releases/individual/lib/libXau-${pkgver}.tar.xz"{,.sig})
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
sha256sums=('8be6f292334d2f87e5b919c001e149a9fdc27005d6b3e053862ac6ebbf1a0c0a'
  'SKIP')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXau-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXau-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "libXau-${pkgver}"

  install -Dm644 "libXau-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/libxau/COPYING"

}
