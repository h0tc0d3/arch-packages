pkgname=polkit+clang
pkgver=121
pkgrel=2
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
provides=('polkit' "polkit=${pkgver}" libpolkit-{agent,gobject}-1.so)
conflicts=('polkit')
depends=(glib2 pam expat systemd duktape+clang)
makedepends=(meson gtk-doc gobject-introspection git)
checkdepends=(python-dbusmock)
backup=(etc/pam.d/polkit-1)
_commit=827b0ddac5b1ef00a47fca4526fcf057bee5f1db
source=(
  "git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=${_commit}"
)
sha256sums=('SKIP')

_llvm=0

pkgver() {

  cd "${srcdir:?}/polkit" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/polkit directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'

}

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup polkit build \
    --prefix=/usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D examples=true \
    -D gtk_doc=true \
    -D man=true \
    -D os_type=redhat \
    -D session_tracking=libsystemd-login \
    -D tests=true \
    -D js_engine=duktape

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs -t 3

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

  install -d -o root -g 102 -m 750 "${pkgdir:?}"/{etc,usr/share}/polkit-1/rules.d
  mv "${pkgdir:?}"/{etc,usr/share}/polkit-1/rules.d/50-default.rules

  install -Dm644 /dev/stdin "${pkgdir:?}/usr/lib/sysusers.d/${pkgname}.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END

}
