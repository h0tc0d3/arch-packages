pkgname=kservice+clang
pkgver=5.90.0
pkgrel=1
pkgdesc='Advanced plugin and service introspection'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
provides=('kservice' "kservice=${pkgver}")
conflicts=('kservice')
depends=(ki18n kconfig kdbusaddons kconfig kcoreaddons)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/kservice-${pkgver}.tar.xz"{,.sig})
sha256sums=('ad4d864f10dcb9b770ab7a1aa44c1024a469c51ee5c89b6ad997d481d7423e88'
  'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kservice-${pkgver} \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
