pkgname=krita+clang
pkgver=5.1.1
pkgrel=2
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
provides=('krita' "krita=${pkgver}")
conflicts=('krita')
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons qt5-svg qt5-multimedia quazip
  gsl libraw exiv2 openexr fftw openjpeg2 opencolorio libwebp hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen vc poppler-qt5 python-pyqt5 libheif
  qt5-tools sip kseexpr libmypaint git)
optdepends=(
  'poppler-qt5: PDF filter' 'ffmpeg: to save animations'
  'python-pyqt5: for the Python plugins' 'libheif: HEIF filter'
  'kseexpr: SeExpr generator layer' 'kimageformats: PSD support'
  'libmypaint: support for MyPaint brushes'
  'libjxl: JPEG-XL filter'
)

_commit=05d6ae2aac46cf59a62a0886cddccd05db979875
source=(
  "krita::git+https://invent.kde.org/graphics/krita.git#commit=${_commit}"
  krita-xsimd-9.patch
)

sha256sums=('SKIP'
  '84767e7da9ad861b5a95550f3cd06f45768f8f39eb4a9ce8cdb674163e763234')
validpgpkeys=(
  '05D00A8B73A686789E0A156858B9596C722EA3BD' # Boudewijn Rempt <foundation@krita.org>
  'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F' # Dmitry Kazakov (main key) <dimula73@gmail.com>
)

_llvm=0

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/krita" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/krita directory! Prepare Failed! \E[0m"
    exit 1
  )

  _patch krita-xsimd-9.patch # Support xsimd 9
  # Remove git hash from version string
  sed -i -e '/get_git_head_hash(GIT_SHA1)/,+12 d' CMakeLists.txt

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    #LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S krita \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DFOUNDATION_BUILD=ON \
    -DKF5Crash=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
