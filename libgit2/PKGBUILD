pkgname=libgit2+clang
pkgver=1.5.0
pkgrel=1
epoch=1
pkgdesc="A linkable library for Git"
arch=(x86_64)
url="https://github.com/libgit2/libgit2"
license=(GPL2)
depends=(glibc http-parser openssl pcre zlib libssh2.so)
makedepends=(cmake libssh2 python)
provides=(libgit2 "libgit2=${pkgver}" libgit2.so)
conflicts=(libgit2)
source=(
  "libgit2-${pkgver}.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
  libgit2-1.5.0-remove_http-parse_incompatible_tests.patch
)

sha256sums=('8de872a0f201b33d9522b817c92e14edb4efad18dae95cf156cf240b2efff93e'
  '8e232d3ac6ba756d6055aaccca9d5704417fe77f53ad91898ffbe1b1439523aa')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi
  # remove tests that are only compatible with the (modified) vendored version
  # of http-parser, but not with upstream http-parser
  patch -d libgit2-${pkgver} -Np1 -i "${srcdir:?}/libgit2-1.5.0-remove_http-parse_incompatible_tests.patch"

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -S libgit2-${pkgver} -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_HTTP_PARSER=system \
    -DUSE_SSH=ON \
    -DTHREADSAFE=ON \
    -Wno-dev

  cmake --build build

}

check() {

  # NOTE: disable tests requiring the internet and relying on non-existent resources
  ctest --test-dir build --output-on-failure -E "invasive|online"

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build
  install -vDm 644 libgit2-${pkgver}/{AUTHORS,README.md} -t "${pkgdir:?}/usr/share/doc/libgit2"

}
