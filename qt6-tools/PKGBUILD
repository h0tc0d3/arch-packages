pkgname=qt6-tools+clang
_qtver=6.2.3
pkgver=${_qtver/-/}
pkgrel=4
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='A cross-platform application and UI framework (Development Tools, QtHelp)'
provides=("qt6-tools" "qt6-tools=${pkgver}")
conflicts=("qt6-tools")
depends=(qt6-base)
makedepends=(cmake qt6-declarative clang llvm)
optdepends=('clang: for qdoc and lupdate')
groups=(qt6)
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/qttools-everywhere-src-${_qtver}.tar.xz")
sha256sums=('9aadbd5a14fad8874d2aa76be79652ec5ed81d31d49b3eff245ebeffc7d8ac08')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qttools-everywhere-src-${_qtver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_PUBLICBINDIR=usr/bin

  make -C build

}

package() {

  DESTDIR="${pkgdir:?}" make -C build install

  install -d "${pkgdir:?}/usr/bin"
  while IFS=$'\n' read -r _line; do
    ln -s "${_line%% *}" "${pkgdir:?}/${_line#* }"
  done <"${srcdir:?}/build/user_facing_tool_links.txt"

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt6-base "${pkgdir:?}/usr/share/licenses/qt6-tools"

}
