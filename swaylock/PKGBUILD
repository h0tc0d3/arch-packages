pkgname=swaylock+clang
pkgver=1.6
pkgrel=2
license=("MIT")
pkgdesc="Screen locker for Wayland"
makedepends=(
  'meson'
  'scdoc'
  'wayland-protocols'
  'git' # To make the version string
)
depends=(
  'cairo'
  'libxkbcommon'
  'pam'
  'wayland'
  'gdk-pixbuf2' # For images other than PNG
)
provides=('swaylock' "swaylock=${pkgver}")
conflicts=('swaylock')
arch=('x86_64')
url="https://github.com/swaywm/swaylock"
backup=('etc/pam.d/swaylock')
source=(
  "swaylock-${pkgver}.tar.gz::https://github.com/swaywm/swaylock/archive/${pkgver}.tar.gz"
  "https://github.com/swaywm/swaylock/releases/download/${pkgver}/swaylock-${pkgver}.tar.gz.sig"
)
sha256sums=('b9181e15d8166bba3a29dabb392cfe94568e4ceb358d388a812cd28c3f61bbe3'
  'SKIP')
validpgpkeys=(
  "9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A" # Drew DeVault
  "34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48" # Simon Ser
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "swaylock-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm644 "swaylock-${pkgver}/LICENSE" -t "${pkgdir:?}/usr/share/licenses/swaylock"
  install -Dm644 "swaylock-${pkgver}/README.md" -t "${pkgdir:?}/usr/share/doc/swaylock"

}
