pkgbase=gdk-pixbuf2+clang
pkgname=(gdk-pixbuf2+clang gdk-pixbuf2-docs+clang)
pkgver=2.42.9
pkgrel=1
pkgdesc="An image loading library"
url="https://wiki.gnome.org/Projects/GdkPixbuf"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libpng libtiff libjpeg shared-mime-info)
makedepends=(gobject-introspection git meson gi-docgen python-docutils)
optdepends=(
  "libwmf: Load .wmf and .apm"
  "libopenraw: Load .dng, .cr2, .crw, .nef, .orf, .pef, .arw, .erf, .mrw, and .raf"
  "libavif: Load .avif"
  "libheif: Load .heif, .heic, and .avif"
  "libjxl: Load .jxl"
  "librsvg: Load .svg, .svgz, and .svg.gz"
  "webp-pixbuf-loader: Load .webp"
)
_commit=9b74d8dced44955b778fa1eac4fd0d29d5809e9d # tags/2.42.9^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#commit=${_commit}"
  gdk-pixbuf-query-loaders.hook
)
sha256sums=('SKIP'
  '9fb71d95e6a212779eb0f88dde5a3cee0df7f4d9183f8f9ce286f8cdc14428f0')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "gdk-pixbuf" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D builtin_loaders=all \
    -D gtk_doc=true \
    -D installed_tests=false

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package_gdk-pixbuf2+clang() {

  provides=(libgdk_pixbuf-2.0.so "gdk-pixbuf2" "gdk-pixbuf2=${pkgver}")
  conflicts=("gdk-pixbuf2")
  install=gdk-pixbuf2.install

  meson install -C build --destdir "${pkgdir:?}"

  mkdir -p doc/usr/share
  mv {"${pkgdir:?}",doc}/usr/share/doc

  install -Dt "${pkgdir:?}/usr/share/libalpm/hooks" -m644 *.hook

}

package_gdk-pixbuf2-docs+clang() {

  provides=("pixbuf2-docs" "pixbuf2-docs=${pkgver}")
  conflicts=("pixbuf2-docs")

  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "${pkgdir:?}"

}
