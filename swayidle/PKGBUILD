pkgname=swayidle+clang
pkgver=1.7.1
pkgrel=1
license=('MIT')
pkgdesc="Idle management daemon for Wayland"
makedepends=(
  'meson'
  'scdoc'
  'wayland-protocols'
)
depends=(
  'wayland'
  'systemd'
)
arch=('x86_64')
provides=('swayidle' "swayidle=${pkgver}")
conflicts=('swayidle')
url="https://github.com/swaywm/swayidle"
source=(
  "swayidle-${pkgver}.tar.gz::https://github.com/swaywm/swayidle/archive/${pkgver}.tar.gz"
  "https://github.com/swaywm/swayidle/releases/download/${pkgver}/swayidle-${pkgver}.tar.gz.sig"
)
sha256sums=('6aa07093be553473726e1ef601dbc5e57a3ea2ac864ef2d7afce1f971ea760a6'
  'SKIP')
validpgpkeys=(
  '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48' # Simon Ser
  '9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A' # Drew DeVault
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "swayidle-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D logind=enabled \
    -D man-pages=enabled \
    -D logind-provider=systemd

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm644 "swayidle-${pkgver}/LICENSE" -t "${pkgdir:?}/usr/share/licenses/swayidle"
  install -Dm644 "swayidle-${pkgver}/README.md" -t "${pkgdir:?}/usr/share/doc/swayidle"

}
