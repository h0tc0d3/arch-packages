pkgname=libidn+clang
pkgver=1.41
pkgrel=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications"
url="https://www.gnu.org/software/libidn/"
arch=('x86_64')
license=('GPL3' 'LGPL')
provides=("libidn" "libidn=${pkgver}")
conflicts=("libidn")
depends=('glibc')
source=("https://ftp.gnu.org/gnu/libidn/libidn-${pkgver}.tar.gz"{,.sig})
sha256sums=('884d706364b81abdd17bee9686d8ff2ae7431c5a14651047c68adf8b31fd8945'
  'SKIP')
validpgpkeys=(
  '9AA9BDB11BB1B99A21285A330664A76954265E8C' # Simon Josefsson <simon@josefsson.org>
  'B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE' # Simon Josefsson <simon@josefsson.org>
  '1CB27DBC98614B2D5841646D08302DB6A2670428' # Tim RÃ¼hsen <tim.ruehsen@gmx.de>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libidn-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libidn-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

check() {

  cd "${srcdir:?}/libidn-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libidn-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/libidn-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libidn-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  # doc html installation - gtk-doc isn't supported anymore - FS#55151
  mkdir -p "${pkgdir:?}/usr/share/gtk-doc/html/${pkgname}"
  cp -r -v doc/reference/html/* "${pkgdir:?}/usr/share/gtk-doc/html/${pkgname}"

}
