pkgbase=wayland+clang
pkgname=(wayland+clang wayland-docs+clang)
pkgver=1.21.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='https://wayland.freedesktop.org/'
license=('MIT')
depends=('glibc' 'libffi' 'expat' 'libxml2')
makedepends=('meson' 'libxslt' 'doxygen' 'xmlto' 'graphviz' 'docbook-xsl')
validpgpkeys=(
  'C7223EBE4EF66513B892598911A30156E0E67611' # Bryce Harrington
  'C0066D7DB8E9AC6844D728715E54498E697F11D7' # Derek Foreman
  '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48' # emersion <contact@emersion.fr>
)
source=("https://gitlab.freedesktop.org/wayland/wayland/-/releases/${pkgver}/downloads/wayland-${pkgver}.tar.xz"{,.sig})
sha256sums=('6dc64d7fc16837a693a51cfdb2e568db538bfdc9f457d4656285bb9594ef11ac'
  'SKIP')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup wayland-${pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package_wayland+clang() {

  provides=(wayland "wayland=${pkgver}" libwayland-{client,cursor,egl,server}.so)
  conflicts=(wayland)

  meson install -C build --destdir "${pkgdir:?}"

  mkdir -p docs/share
  mv "${pkgdir:?}/usr/share/"{doc,man} docs/share
  install -Dm 644 "wayland-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/${pkgname:?}/COPYING"

}

package_wayland-docs+clang() {

  pkgdesc+=" (documentation)"
  provides=(wayland-docs "wayland-docs=${pkgver}")
  conflicts=(wayland-docs)

  mv docs "${pkgdir:?}/usr"
  install -Dm 644 "wayland-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/${pkgname:?}/COPYING"

}
