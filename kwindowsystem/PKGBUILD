pkgname=kwindowsystem+clang
pkgver=5.97.0
pkgrel=1
pkgdesc='Access to the windowing system'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
provides=("kwindowsystem" "kwindowsystem=${pkgver}")
conflicts=("kwindowsystem")
depends=(qt5-x11extras)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen)
groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/kwindowsystem-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('b822a796379a1ce03a4a9fad64aa6289ba7abd84e3b137af31d8043b9c5efef1'
  'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kwindowsystem-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
