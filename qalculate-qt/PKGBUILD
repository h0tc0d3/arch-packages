pkgname=qalculate-qt+clang
pkgver=4.3.0
pkgrel=1
pkgdesc='Qt frontend for libqalculate'
arch=(x86_64)
url='https://qalculate.github.io/'
license=(GPL)
provides=(qalculate-qt "qalculate-qt=${pkgver}")
conflicts=(qalculate-qt)
depends=(libqalculate qt5-svg hicolor-icon-theme)
makedepends=(qt5-tools)
source=("https://github.com/Qalculate/qalculate-qt/releases/download/v${pkgver}/qalculate-qt-${pkgver}.tar.gz")
sha256sums=('add2c06b7909b99de2db8853ba48af5556ac51358423faeb1c03931fb6c446bf')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/qalculate-qt-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to qalculate-qt-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin -std=c++17"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  sed -Ei "s/<\!--(.*)-->/\1/g" flags.qrc

  qmake PREFIX=/usr
  make

}

package() {

  make INSTALL_ROOT="${pkgdir:?}" install -C "qalculate-qt-${pkgver}"

}
