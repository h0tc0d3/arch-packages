pkgbase=dbus+clang
pkgname=(dbus+clang dbus-docs+clang)
pkgver=1.14.4
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
depends=(systemd-libs expat audit libselinux)
makedepends=(systemd xmlto docbook-xsl python yelp-tools doxygen git autoconf-archive)
source=(
  "git+https://gitlab.freedesktop.org/dbus/dbus.git?signed#tag=dbus-${pkgver}"
  dbus-reload.hook
  no-fatal-warnings.diff
)
sha256sums=('SKIP'
  'd636205622d0ee3b0734360225739ef0c7ad2468a09489e6ef773d88252960f3'
  '721d60b12e98ec7c4f0d0b0a8eabb0d1a7450f8be9dfc4da025bd8833740f1a5')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90') # Simon McVittie <simon.mcvittie@collabora.co.uk>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/dbus" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/dbus directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Allow us to enable checks without them being fatal
  patch -Np1 -i ../no-fatal-warnings.diff

  NOCONFIGURE=1 ./autogen.sh

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/dbus" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/dbus directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/dbus-1.0 \
    runstatedir=/run \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-inotify \
    --enable-libaudit \
    --enable-selinux \
    --enable-systemd \
    --enable-user-session \
    --enable-xml-docs \
    --enable-doxygen-docs \
    --enable-ducktype-docs \
    --disable-static \
    --without-x

  make

}

check() {

  make -C dbus check

}

package_dbus+clang() {

  depends+=(libsystemd.so libaudit.so)
  provides=('dbus' "dbus=${pkgver}" 'libdbus' 'libdbus-1.so')
  conflicts=('dbus' 'libdbus')
  replaces=('libdbus')

  DESTDIR="${pkgdir:?}" make -C dbus install

  rm -r "${pkgdir:?}"/{etc,var}

  # We have a pre-assigned uid (81)
  echo 'u dbus 81 "System Message Bus"' |
    install -Dm644 /dev/stdin "${pkgdir:?}/usr/lib/sysusers.d/dbus.conf"

  install -Dt "${pkgdir:?}/usr/share/libalpm/hooks" -m644 *.hook
  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname:?}" -m644 dbus/COPYING

  # Split docs
  mv "${pkgdir:?}/usr/share/doc" "${srcdir:?}"

}

package_dbus-docs+clang() {

  pkgdesc+=" (documentation)"
  provides=('dbus-docs' "dbus-docs=${pkgver}")
  conflicts=('dbus-docs')
  depends=(dbus+clang)

  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname:?}" -m644 dbus/COPYING
  mv doc "${pkgdir:?}/usr/share"

}
