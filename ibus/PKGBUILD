pkgbase=ibus+clang
pkgname=(ibus+clang libibus+clang)
pkgver=1.5.27
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('x86_64')
url="https://github.com/ibus/ibus/wiki"
license=('LGPL')
depends=('dconf' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject'
  'iso-codes' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'gnome-common' 'gtk-doc' 'gtk4' 'gtk2'
  'qt5-base' 'unicode-cldr' 'unicode-character-database' 'unicode-emoji')
options=('!emptydirs')
source=("https://github.com/ibus/ibus/releases/download/${pkgver}/ibus-${pkgver}.tar.gz")
sha256sums=('6efbda5adb96f607cf7108d1e270962c0729a59c9ea6d58eea2dde0e3cbb97df')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/ibus-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/ibus-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  NOCONFIGURE=1 ./autogen.sh

}

build() {

  cd "${srcdir:?}/ibus-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/ibus-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --enable-dconf \
    --enable-wayland \
    --enable-gtk-doc \
    --enable-gtk4 \
    --disable-memconf \
    --enable-ui \
    --disable-python2 \
    --with-python=python3 \
    --with-ucd-dir=/usr/share/unicode/

  make

}

package_ibus+clang() {

  depends+=("libibus=$pkgver")
  provides=('ibus' "ibus=${pkgver}")
  conflicts=('ibus')

  make -C "ibus-${pkgver}" DESTDIR="${pkgdir:?}" install
  make -C "ibus-${pkgver}/src" DESTDIR="${pkgdir:?}" uninstall
  make -C "ibus-${pkgver}/bindings" DESTDIR="${pkgdir:?}" uninstall
  make -C "ibus-${pkgver}" DESTDIR="${pkgdir:?}" uninstall-pkgconfigDATA

}

package_libibus+clang() {

  pkgdesc="IBus support library"
  depends=(libg{lib,object,io}-2.0.so)
  provides=('libibus-1.0.so' 'libibus' "libibus=${pkgver}")
  conflicts=('libibus')

  make -C "ibus-${pkgver}/src" DESTDIR="${pkgdir:?}" install
  make -C "ibus-${pkgver}/bindings" DESTDIR="${pkgdir:?}" install
  make -C "ibus-${pkgver}" DESTDIR="${pkgdir:?}" install-pkgconfigDATA

}
