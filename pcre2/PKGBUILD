pkgname=pcre2+clang
pkgver=10.40
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions. 2nd version'
arch=('x86_64')
url='https://www.pcre.org/'
license=('BSD')
provides=(pcre2 "pcre2=${pkgver}")
conflicts=(pcre2)
depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'bash')
source=("https://github.com/PhilipHazel/pcre2/releases/download/pcre2-${pkgver}/pcre2-${pkgver}.tar.bz2"{,.sig})
sha256sums=('14e4b83c4783933dc17e964318e6324f7cae1bc75d8f3c79bc6969f00c159d68'
  'SKIP')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/pcre2-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/pcre2-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-jit \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libreadline

  make

}

check() {

  make -j1 check -C "pcre2-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "pcre2-${pkgver}"

  install -Dm644 "pcre2-${pkgver}/LICENCE" "${pkgdir:?}/usr/share/licenses/pcre2/LICENSE"

}
