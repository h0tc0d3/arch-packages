pkgname=qt6-multimedia+clang
_qtver=6.3.1
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
provides=("qt6-multimedia" "qt6-multimedia=${pkgver}")
conflicts=("qt6-multimedia")
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=(qt6-base gst-plugins-base)
makedepends=(cmake qt6-declarative qt6-shadertools)
optdepends=('qt6-declarative: QML bindings'
  'gst-plugins-good: camera support, additional plugins'
  'gst-plugins-bad: camera support, additional plugins'
  'gst-plugins-ugly: additional plugins'
  'gst-libav: ffmpeg plugin')
groups=(qt6)
source=("https://ftp.fau.de/qtproject/archive/qt/${pkgver%.*}/${_qtver}/submodules/qtmultimedia-everywhere-src-${_qtver}.tar.xz")
sha256sums=('7e03242aadd634ff2d9fcf08948290f03da3d9a5012369d908da89f82b1d7336')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qtmultimedia-everywhere-src-${_qtver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt6-base "${pkgdir:?}/usr/share/licenses/qt6-multimedia"

}
