pkgname=krb5+clang
_pkgvermajor=1.19
pkgver=1.19.2
pkgrel=2
pkgdesc='The Kerberos network authentication system'
url='https://web.mit.edu/kerberos/'
arch=('x86_64')
license=('custom')
depends=('glibc' 'e2fsprogs' 'libss.so' 'libcom_err.so' 'libldap' 'keyutils')
makedepends=('perl')
provides=(
  "krb5" "krb5=${pkgver}"
  libgssapi_krb5.so
  libgssrpc.so
  libk5crypto.so
  libkadm5clnt_mit.so
  libkadm5srv_mit.so
  libkdb5.so
  libkdb_ldap.so
  libkrad.so
  libkrb5.so
  libkrb5support.so
  libverto.so
)
conflicts=("krb5")
backup=(
  'etc/krb5.conf'
  'var/lib/krb5kdc/kdc.conf'
)
options=('!emptydirs')
source=(https://web.mit.edu/kerberos/dist/krb5/${_pkgvermajor}/krb5-${pkgver}.tar.gz{,.asc}
  krb5-config_LDFLAGS.patch
  krb5-kadmind.service
  krb5-kdc.service
  krb5-kpropd.service
  krb5-kpropd@.service
  krb5-kpropd.socket)
sha512sums=('b90d6ed0e1e8a87eb5cb2c36d88b823a6a6caabf85e5d419adb8a930f7eea09a5f8491464e7e454cca7ba88be09d19415962fe0036ad2e31fc584f9fc0bbd470'
  'SKIP'
  '5a3782ff17b383f8cd0415fd13538ab56afd788130d6ad640e9f2682b7deaae7f25713ce358058ed771091040dccf62a3bc87e6fd473d505ec189a95debcc801'
  'ae1fa980e8e30a83dfef7fe233be70a9ec530ebaffc344a0e7eba61e7de4c800421b45cf203f1e526cc8351754038d6539184b30aa049a567e2a9e80f0d39841'
  'b137ff0154f9af4891e9e78cec692af47ecfd92ca9ce6e00b40ef137d942ba04e8caf483fc5d464b9559ad4a5c4e75ec57b6eab7fc35181115ca1606c0b316c1'
  'b57bbe55c19c92a04cd3e8ad569adcc5aedd4246075b2e6dbcc363e523853332a3e7650be85098b91e495799f8b728ea550495ab303f46ccd9298a2260120b2f'
  'a31ed42e988fed892dd3f1ca1942c3cd4bf391d894c9cebe5e91f52797392261e129727590ae06e2f727bb7b0f8d73a7aa35e8b983d6bad77f22a099091ee944'
  '4e7ae175425e0787a1d5ff959471a88bf5af4cd6e213dc6d4048902fab7547c1186a082370b523f9549f5096acfab1fb03b4839e42bd80dc539130ae4bb3ea55')
validpgpkeys=('2C732B1C0DBEF678AB3AF606A32F17FD0055C305' # Tom Yu <tlyu@mit.edu>
  'C4493CB739F4A89F9852CBC20CBA08575F8372DF')            # Greg Hudson <ghudson@mit.edu>

_llvm=0

prepare() {

  cd "${srcdir:?}/krb5-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/krb5-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  # cf https://bugs.gentoo.org/show_bug.cgi?id=448778
  patch -Np1 <"${srcdir:?}/krb5-config_LDFLAGS.patch"

  # FS#25384
  sed -i "/KRB5ROOT=/s/\/local//" src/util/ac_check_krb5.m4

}

build() {

  cd "${srcdir:?}/krb5-${pkgver}/src" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/krb5-${pkgver}/src directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  CFLAGS="${CFLAGS} -fPIC -fno-strict-aliasing"
  CPPFLAGS="${CPPFLAGS} -I/usr/include/et"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --enable-shared \
    --with-system-et \
    --with-system-ss \
    --disable-rpath \
    --without-tcl \
    --enable-dns-for-realm \
    --with-ldap \
    --without-system-verto

  make

}

package() {

  cd "${srcdir:?}/krb5-${pkgver}/src" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/krb5-${pkgver}/src directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" EXAMPLEDIR=/usr/share/doc/krb5/examples install

  # Fix FS#29889
  install -m 644 "plugins/kdb/ldap/libkdb_ldap/kerberos."{ldif,schema} \
    "${pkgdir:?}/usr/share/doc/krb5/examples"

  install -Dpm 644 config-files/krb5.conf -t "${pkgdir:?}/etc"
  install -Dpm 644 config-files/kdc.conf -t "${pkgdir:?}/var/lib/krb5kdc"

  install -Dm 644 util/ac_check_krb5.m4 -t "${pkgdir:?}/usr/share/aclocal"

  install -Dm 644 "${srcdir:?}/krb5-${pkgver}/NOTICE" \
    "${pkgdir:?}/usr/share/licenses/krb5/LICENSE"

  # systemd stuff
  install -Dm 644 "${srcdir:?}/krb5-"{kadmind.service,kdc.service,kpropd.service,kpropd@.service,kpropd.socket} \
    -t "${pkgdir:?}/usr/lib/systemd/system"

}
