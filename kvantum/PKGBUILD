pkgname=kvantum+clang
pkgver=1.0.2
pkgrel=1
pkgdesc="SVG-based theme engine for Qt5/6 (including config tool and extra themes)"
arch=(x86_64)
url="https://github.com/tsujan/Kvantum"
license=(GPL)
provides=(kvantum-qt5 kvantum)
conflicts=(kvantum-qt5 kvantum)
replaces=(kvantum-qt5 kvantum)
depends=(qt5-svg kwindowsystem hicolor-icon-theme)
makedepends=(cmake qt5-tools qt6-tools qt6-svg)
optdepends=('qt6-svg: Qt6 style')
source=("https://github.com/tsujan/Kvantum/releases/download/V${pkgver}/Kvantum-${pkgver}.tar.xz"{,.asc})
sha256sums=('a3445a83f2aae12f62814b41764b4e1409a6f1114d7fad355f9ccb7a318d30ec'
  'SKIP')
validpgpkeys=(19DFDF3A579BD509DBB572D8BE793007AD22DF7E) # Pedram Pourang <tsujan2000@gmail.com>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  # Fix Qt6 build with Qt5 installed
  sed 's|Qt6 Qt5|Qt6|' -i "Kvantum-${pkgver}/Kvantum/"*/CMakeLists.txt

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build5 -S "Kvantum-${pkgver}/Kvantum" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build5

  cmake -B build6 -S "Kvantum-${pkgver}/Kvantum" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=OFF

  cmake --build build6

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build5
  DESTDIR="${pkgdir:?}" cmake --install build6

}
