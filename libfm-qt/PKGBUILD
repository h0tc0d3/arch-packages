pkgname=libfm-qt+clang
pkgver=1.1.0
pkgrel=2
pkgdesc="Core library of PCManFM-Qt (Qt binding for libfm)"
arch=("x86_64")
url="https://github.com/lxqt/libfm-qt"
license=("LGPL2.1")
provides=("libfm-qt.so" "libfm-qt" "libfm-qt=${pkgver}")
conflicts=("libfm-qt")
depends=("menu-cache" "libexif" "qt5-x11extras")
makedepends=("lxqt-build-tools")
optdepends=(
  "org.freedesktop.secrets: allow to remember mount passwords"
)
source=(
  "https://github.com/lxqt/libfm-qt/releases/download/${pkgver}/libfm-qt-${pkgver}.tar.xz"{,.asc}
)
sha256sums=('a5eecf0c9725c1a32470eaf539989563ba53ea6ed916454cc47e940279c04e54'
  'SKIP')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373" # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3" # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E" # Pedram Pourang <tsujan2000@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "libfm-qt-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
