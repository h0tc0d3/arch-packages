pkgbase=pango+clang
pkgname=(pango+clang pango-docs+clang)
pkgver=1.50.0
pkgrel=2
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(libthai cairo libxft harfbuzz fribidi)
makedepends=(gobject-introspection help2man git meson gi-docgen)
checkdepends=(ttf-dejavu cantarell-fonts)
_commit=e80e224baabc7bf22971d0a97c8b31d32fa8ca1c # tags/1.50.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/pango.git#commit=${_commit}"
  pango-test-fix.diff
)
sha256sums=('SKIP'
  'e1c0638305c0f46653bd60b5dee0b3445dd6a6b9aff8cee70be7759c21ba1037')

_extra_lto_flag=""

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  cd "${srcdir:?}/pango" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/pango directory! Prepare Failed! \E[0m"
    exit 1
  )

  # https://bugs.archlinux.org/task/72927
  git cherry-pick -n f5fdfe4e16b7da761d918f40bb82dad52513c9ff

  _patch pango-test-fix.diff

}

pkgver() {

  cd "${srcdir:?}/pango" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/pango directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-/+/g'

}

build() {

  meson setup pango build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D default_library=both \
    -D introspection=enabled \
    -D gtk_doc=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs || true

}

package_pango+clang() {

  provides=(pango libpango{,cairo,ft2,xft}-1.0.so)
  conflicts=('pango' "pango=${pkgver}")

  meson install -C build --destdir "${pkgdir:?}"

  mkdir -p doc/usr/share
  mv {"${pkgdir:?}",doc}/usr/share/doc

}

package_pango-docs+clang() {

  pkgdesc+=" (documentation)"
  depends=('pango+clang')
  provides=('pango-docs' "pango-docs=${pkgver}")
  conflicts=('pango-docs')

  mv doc/* "${pkgdir:?}"

}
