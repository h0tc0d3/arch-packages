pkgname=kinit+clang
pkgver=5.95.0
pkgrel=1
pkgdesc='Process launcher to speed up launching KDE applications'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio)
provides=('kinit' "kinit=${pkgver}")
conflicts=('kinit')
makedepends=(extra-cmake-modules kdoctools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/kinit-${pkgver}.tar.xz"{,.sig})
install=kinit.install
sha256sums=('cc5be7ad9c11aeff434a81bfca26400154a835f7f4a4a558118c1271a49bf07b'
  'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kinit-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
