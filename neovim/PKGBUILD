pkgname=neovim+clang
pkgver=0.7.0
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=('x86_64')
url='https://neovim.io'
backup=('etc/xdg/nvim/sysinit.vim')
license=('custom:neovim')
provides=('vim-plugin-runtime' 'neovim')
conflicts=('neovim')
depends=('libtermkey' 'libuv' 'msgpack-c' 'unibilium' 'libvterm' 'luajit' 'libluv' 'tree-sitter')
makedepends=('cmake' 'lua51-mpack' 'lua51-lpeg' 'gperf')
optdepends=('python-neovim: for Python 3 plugin support (see :help python)'
  'xclip: for clipboard support on X11 (or xsel) (see :help clipboard)'
  'xsel: for clipboard support on X11 (or xclip) (see :help clipboard)'
  'wl-clipboard: for clipboard support on wayland (see :help clipboard)')
source=(
  "https://github.com/neovim/neovim/archive/v${pkgver}/neovim-${pkgver}.tar.gz"
  fix-build.patch
)
sha256sums=('792a9c55d5d5f4a5148d475847267df309d65fb20f05523f21c1319ea8a6c7df'
  'bceb977645b38d4cf0c6c3fcb87ebaaf12b81553da5981e472d80fd3f1c8b8a0')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -Np1 -d "neovim-${pkgver}" -i "${srcdir:?}/fix-build.patch"

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -S "neovim-${pkgver}" \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_BUNDLED=OFF

  cmake --build build

}

check() {

  ./build/bin/nvim --version
  ./build/bin/nvim --headless -u NONE -i NONE -c ':quit'

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  cd "${srcdir:?}/neovim-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/neovim-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  install -Dm644 LICENSE.txt "${pkgdir:?}/usr/share/licenses/neovim"
  install -Dm644 runtime/nvim.desktop "${pkgdir:?}/usr/share/applications/nvim.desktop"
  install -Dm644 runtime/nvim.appdata.xml "${pkgdir:?}/usr/share/metainfo/nvim.appdata.xml"
  install -Dm644 runtime/nvim.png "${pkgdir:?}/usr/share/pixmaps/nvim.png"

  # Make Arch vim packages work
  mkdir -p "${pkgdir:?}/etc/xdg/nvim"
  echo "\" This line makes pacman-installed global Arch Linux vim packages work." >"${pkgdir:?}/etc/xdg/nvim/sysinit.vim"
  echo "source /usr/share/nvim/archlinux.vim" >>"${pkgdir:?}/etc/xdg/nvim/sysinit.vim"

  mkdir -p "${pkgdir:?}/usr/share/vim"
  echo "set runtimepath+=/usr/share/vim/vimfiles" >"${pkgdir:?}/usr/share/nvim/archlinux.vim"

}
