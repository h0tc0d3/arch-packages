pkgname=qt6-quick3d+clang
_qtver=6.3.2
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3)
pkgdesc='Qt module and API for defining 3D content in Qt Quick'
provides=("qt6-quick3d" "qt6-quick3d=${pkgver}")
conflicts=("qt6-quick3d")
depends=(qt6-quicktimeline)
makedepends=(cmake qt6-shadertools assimp)
optdepends=('qt6-shadertools: for shadergen' 'assimp: assimp import plugin')
groups=(qt6)
source=("https://ftp.fau.de/qtproject/archive/qt/${pkgver%.*}/${_qtver}/submodules/qtquick3d-everywhere-src-${_qtver}.tar.xz")
sha256sums=('1da0aa3db99bd2eed017c9bc9348fc3a52c21e8b2684af16e784e2cc86a29c03')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qtquick3d-everywhere-src-${_qtver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt6-base "${pkgdir:?}/usr/share/licenses/qt6-quick3d"

}
