pkgname=kio-extras+clang
pkgver=21.12.1
pkgrel=1
pkgdesc='Additional components to increase the functionality of KIO'
arch=(x86_64)
url='https://www.kde.org/'
license=(LGPL)
provides=("kio-extras" "kio-extras=${pkgver}")
conflicts=("kio-extras")
depends=(kio kdnssd libssh smbclient libmtp phonon-qt5 syntax-highlighting kdsoap-ws-discovery-client libxcursor)
makedepends=(extra-cmake-modules kdoctools gperf taglib libappimage openexr kactivities-stats)
optdepends=('qt5-imageformats: thumbnails for additional image formats' 'perl: info kioslave'
  'kimageformats: thumbnails for additional image formats' 'taglib: audio file thumbnails'
  'libappimage: AppImage thumbnails' 'icoutils: Windows executable thumbnails'
  'openexr: EXR format thumbnails' 'kactivities-stats: recently used kioslave')
groups=(kde-applications kde-network)
source=(
  "https://download.kde.org/stable/release-service/${pkgver}/src/kio-extras-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('cc7d5465602c0cd26577a30572ebfa3141414828a360f17e6e456a2d6faa505f'
  'SKIP')
validpgpkeys=(
  CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7 # Albert Astals Cid <aacid@kde.org>
  F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87 # Christoph Feck <cfeck@kde.org>
  D81C0CB38EB725EF6691C385BB463350D6EF31EF # Heiko Becker <heiko.becker@kde.org>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kio-extras-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
