pkgname=qt6-declarative+clang
_qtver=6.4.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for QML and JavaScript languages'
depends=(qt6-base)
makedepends=(cmake python at-spi2-core qt6-shadertools)
conflicts=(qt6-quickcontrols2 qt6-declarative)
provides=(qt6-quickcontrols2 "qt6-declarative" "qt6-declarative=${pkgver}")
replaces=(qt6-quickcontrols2)
groups=(qt6)
source=(
  "https://ftp.fau.de/qtproject/archive/qt/${pkgver%.*}/${_qtver}/submodules/qtdeclarative-everywhere-src-${_qtver}.tar.xz"
  "qmlls-crash.patch::https://code.qt.io/cgit/qt/qtdeclarative.git/patch/?id=fb6a62a0"
)
sha256sums=('3434e72fccfa0c929c326790723d05c155f5067746b1ab05cfd7a9ba632c4383'
  'b26df66dac81167760164609f8a4b7118782a613e78aec97a823a441ace561eb')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  patch -d "qtdeclarative-everywhere-src-${_qtver}" -p1 <qmlls-crash.patch # Fix qmlls crashes

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qtdeclarative-everywhere-src-${_qtver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_PUBLICBINDIR=usr/bin

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -d "${pkgdir:?}/usr/bin"
  while IFS=$'\n' read -r _line; do
    ln -s "${_line%% *}" "${pkgdir:?}/${_line#* }"
  done <"${srcdir:?}/build/user_facing_tool_links.txt"

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt6-base "${pkgdir:?}/usr/share/licenses/qt6-declarative"

}
