pkgname=libtorrent-rasterbar+clang
pkgver=2.0.7
pkgrel=2
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
provides=('libtorrent-rasterbar' "libtorrent-rasterbar=${pkgver}")
conflicts=('libtorrent-rasterbar')
depends=('boost-libs' 'openssl')
makedepends=('boost' 'cmake' 'python-setuptools')
options=('!emptydirs')
source=(
  "https://github.com/arvidn/libtorrent/releases/download/v${pkgver}/libtorrent-rasterbar-${pkgver}.tar.gz"
)
sha256sums=('3850a27aacb79fcc4d352c1f02a7a59e0e8322afdaa1f5d58d676c02edfcfa36')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build \
    -S "libtorrent-rasterbar-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -Dpython-bindings=ON \
    -Dboost-python-module-name=python \
    -Dpython-egg-info=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -Dm644 "libtorrent-rasterbar-${pkgver}/COPYING" -t "${pkgdir:?}/usr/share/licenses/libtorrent-rasterbar"

}
