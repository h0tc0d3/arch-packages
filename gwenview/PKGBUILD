pkgname=gwenview+clang
pkgver=22.08.0
pkgrel=1
pkgdesc='A fast and easy to use image viewer'
url='https://apps.kde.org/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
provides=("gwenview" "gwenview=${pkgver}")
conflicts=("gwenview")
groups=(kde-applications kde-graphics)
depends=(kactivities kimageannotator baloo libkdcraw kparts kitemmodels cfitsio phonon-qt5 purpose perl)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
  'kimageformats: support for dds, xcf, exr, psd, and more image formats'
  'kipi-plugins: export to various online services'
  'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=(
  "https://download.kde.org/stable/release-service/${pkgver}/src/gwenview-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('447d1e8a0c19476fc2879a4e406d7d6af79b59f9e2c7aad28f70d484789ff716'
  'SKIP')
validpgpkeys=(
  CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7 # Albert Astals Cid <aacid@kde.org>
  F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87 # Christoph Feck <cfeck@kde.org>
  D81C0CB38EB725EF6691C385BB463350D6EF31EF # Heiko Becker <heiko.becker@kde.org>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S gwenview-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DGWENVIEW_SEMANTICINFO_BACKEND=None \
    -DBUILD_TESTING=OFF

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
