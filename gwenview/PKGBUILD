pkgname=gwenview+clang
pkgver=21.04.2
pkgrel=1
pkgdesc='A fast and easy to use image viewer'
url='https://apps.kde.org/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
provides=("gwenview" "gwenview=${pkgver}")
conflicts=("gwenview")
groups=(kde-applications kde-graphics)
depends=(kactivities baloo libkipi libkdcraw kparts kitemmodels cfitsio phonon-qt5 purpose perl)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
  'kimageformats: support for dds, xcf, exr, psd, and more image formats'
  'kipi-plugins: export to various online services'
  'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=(
  "https://download.kde.org/stable/release-service/${pkgver}/src/gwenview-${pkgver}.tar.xz"
  'lto.cmake'
)
sha256sums=('db062fa4f07c9372534c21b15f2953eecf172c81a1d3ebc227267670f615d47e'
            '7971e5dd106804e615bf270a1d660835ded44f5707263e40a0b402cd1d63348e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7 # Albert Astals Cid <aacid@kde.org>
  F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87             # Christoph Feck <cfeck@kde.org>
  D81C0CB38EB725EF6691C385BB463350D6EF31EF)            # Heiko Becker <heiko.becker@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    cmake -B build -S gwenview-${pkgver} \
      -DCMAKE_TOOLCHAIN_FILE=../lto.cmake \
      -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
      -DBUILD_TESTING=OFF

  else

    cmake -B build -S gwenview-${pkgver} \
      -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
      -DBUILD_TESTING=OFF

  fi

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
