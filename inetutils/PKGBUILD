pkgname=inetutils+clang
pkgver=2.2
pkgrel=1
pkgdesc="A collection of common network programs"
arch=('x86_64')
url="https://www.gnu.org/software/inetutils/"
license=('GPL3')
provides=('inetutils' "inetutils=${pkgver}")
conflicts=('inetutils')
depends=('pam' 'libcap' 'readline' 'ncurses' 'libxcrypt'
  libpam.so libcrypt.so libreadline.so libncursesw.so)
makedepends=('help2man')
backup=('etc/xinetd.d/telnet' 'etc/xinetd.d/talk'
  'etc/xinetd.d/rlogin' 'etc/xinetd.d/rsh'
  'etc/pam.d/rlogin' 'etc/pam.d/rsh')
options=('!emptydirs')
install=inetutils.install
source=("https://ftp.gnu.org/gnu/inetutils/inetutils-${pkgver}.tar.xz"{,.sig}
  'ftpd.service' 'rlogin.pam' 'rlogin@.service' 'rlogin.socket' 'rlogin.xinetd'
  'rsh.pam' 'rsh@.service' 'rsh.socket' 'rsh.xinetd' 'talk.service' 'talk.socket'
  'talk.xinetd' 'telnet@.service' 'telnet.socket' 'telnet.xinetd')
sha256sums=('d547f69172df73afef691a0f7886280fd781acea28def4ff4b4b212086a89d80'
  'SKIP'
  'f1b9b4e57f484070366444a649f1be151d01d5bc965b9b192c242e4b7cc4beeb'
  '428367b148033c7fa865e92bdd73b06cb58e6909488649adebf8d2253a022f1f'
  '6112bcdb595937a8c7940dc158a97fd48b8cce6526a9fb017f347f614b9d6548'
  '4d1c1eef689be2caf26435e38f7e37337fdfd642a0c1e32aa60a9e94b2983df5'
  'c5ff9a299f4b09bc097069e8a4987f6db495083fb7b11443e390cabfdf230165'
  '6317e1e62f0dcf32ac99334bafdc8972b4967048456448ef1e2458219661bf18'
  'c726f53023b42e2b0f0dc12ef5989914bc7050855967b3e5263112bb8e75b0c5'
  'c17a00bbc75edae1131cab216189cc1c0157d4de91d8c9155cc2a496b8b2aa2d'
  '5d8852f3961ea9b1aec7d50b84ddde4746ddbf4f6c5ed67cb419ab56e79d4762'
  '73f368fae50ce6107846f156d667dbc3e60428cc1f0976ab6ca82d34bd8eff82'
  '91f135996131daf07c51cf76756c2064270d9f5b74b863c36055a0b5e08e80c7'
  '0f5799a8d9a8a7ad28ff99ee022b1e742b6e5cfd1ba451339173d6eb75624bab'
  '5df7e1f283366dbf57b7ecd98fbc503aa42bac2fc3084ae98b866ef261d98d55'
  '7f84e6a9ad2d28ddc440aab95359c14c633deb5e9bb7a0df7a46a3d54c1aefc5'
  'b019c8d8130a5dcda5dacd988a7e35e21e34738ff4d6b4cfe39be83a413ec8ac')

# GNU Keyring: https://ftp.gnu.org/gnu/gnu-keyring.gpg
validpgpkeys=(
  '4FBD67621082C4C502448E3B180551BAD95A3C35' # Alfred M. Szmidt <ams@gnu.org>
  '9AA9BDB11BB1B99A21285A330664A76954265E8C' # Simon Josefsson <simon@josefsson.org>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/inetutils-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/inetutils-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --libexec=/usr/bin \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --without-wrap \
    --with-pam \
    --enable-ftp \
    --enable-ftpd \
    --enable-telnet \
    --enable-telnetd \
    --enable-talk \
    --enable-talkd \
    --enable-rlogin \
    --enable-rlogind \
    --enable-rsh \
    --enable-rshd \
    --enable-rcp \
    --enable-hostname \
    --enable-dnsdomainname \
    --disable-rexec \
    --disable-rexecd \
    --disable-tftp \
    --disable-tftpd \
    --disable-ping \
    --disable-ping6 \
    --disable-logger \
    --disable-syslogd \
    --disable-inetd \
    --disable-whois \
    --disable-uucpd \
    --disable-ifconfig \
    --disable-traceroute

  make

}

check() {

  make check -C inetutils-${pkgver}

}

package() {

  make DESTDIR="${pkgdir:?}" install -C inetutils-${pkgver}

  chmod -s "${pkgdir:?}/usr/bin/"{rcp,rlogin,rsh}

  install -D -m644 "${srcdir:?}/telnet.xinetd" "${pkgdir:?}/etc/xinetd.d/telnet"
  install -D -m644 "${srcdir:?}/talk.xinetd" "${pkgdir:?}/etc/xinetd.d/talk"
  install -D -m644 "${srcdir:?}/rlogin.xinetd" "${pkgdir:?}/etc/xinetd.d/rlogin"
  install -D -m644 "${srcdir:?}/rsh.xinetd" "${pkgdir:?}/etc/xinetd.d/rsh"

  install -D -m644 "${srcdir:?}/rlogin.pam" "${pkgdir:?}/etc/pam.d/rlogin"
  install -D -m644 "${srcdir:?}/rsh.pam" "${pkgdir:?}/etc/pam.d/rsh"

  install -D -m644 "${srcdir:?}/ftpd.service" "${pkgdir:?}/usr/lib/systemd/system/ftpd.service"
  install -D -m644 "${srcdir:?}/rlogin@.service" "${pkgdir:?}/usr/lib/systemd/system/rlogin@.service"
  install -D -m644 "${srcdir:?}/rlogin.socket" "${pkgdir:?}/usr/lib/systemd/system/rlogin.socket"
  install -D -m644 "${srcdir:?}/rsh@.service" "${pkgdir:?}/usr/lib/systemd/system/rsh@.service"
  install -D -m644 "${srcdir:?}/rsh.socket" "${pkgdir:?}/usr/lib/systemd/system/rsh.socket"
  install -D -m644 "${srcdir:?}/talk.service" "${pkgdir:?}/usr/lib/systemd/system/talk.service"
  install -D -m644 "${srcdir:?}/talk.socket" "${pkgdir:?}/usr/lib/systemd/system/talk.socket"
  install -D -m644 "${srcdir:?}/telnet@.service" "${pkgdir:?}/usr/lib/systemd/system/telnet@.service"
  install -D -m644 "${srcdir:?}/telnet.socket" "${pkgdir:?}/usr/lib/systemd/system/telnet.socket"

}
