pkgname=libxmu+clang
pkgver=1.1.4
pkgrel=1
pkgdesc="X11 miscellaneous micro-utility library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
provides=(libxmu "libxmu=${pkgver}")
conflicts=(libxmu)
depends=('libxext' 'libxt')
makedepends=('xorg-util-macros')
license=('custom')
source=("${url}/releases/individual/lib/libXmu-${pkgver}.tar.xz"{,.sig})
sha256sums=('210de3ab9c3e9382572c25d17c2518a854ce6e2c62c5f8315deac7579e758244'
  'SKIP')
validpgpkeys=(
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
)
_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXmu-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}libXmu-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static

  make

}

package() {

  cd "${srcdir:?}/libXmu-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}libXmu-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
