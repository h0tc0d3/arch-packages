pkgname=amdvlk+clang
pkgver=2022.Q3.1
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
provides=('vulkan-driver' 'amdvlk' "amdvlk=${pkgver}")
conflicts=('amdvlk')
makedepends=('python' 'wayland' 'libxrandr' 'xorg-server-devel'
  'cmake' 'git' 'clang' 'lld' 'glslang' 'spirv-tools'
  'spirv-headers')
# Please use a script generate-source.py for reduce git requests
source=(
  "amdvlk/xgl::git+https://github.com/GPUOpen-Drivers/xgl#commit=394cbffcf388482354c7874a841a30d386d9abc8"
  "amdvlk/pal::git+https://github.com/GPUOpen-Drivers/pal#commit=8a99ffaa533ee561af7075d413f6052ea37a1a04"
  "amdvlk/llpc::git+https://github.com/GPUOpen-Drivers/llpc#commit=d4763a9dcab538955bc5ad4b5c6e0139d8fe8949"
  "amdvlk/spvgen::git+https://github.com/GPUOpen-Drivers/spvgen#commit=3b61d0ce836d3832e19931be198f6f619206de4c"
  "amdvlk/AMDVLK::git+https://github.com/GPUOpen-Drivers/AMDVLK#commit=f5f3eced60fd0ba2125fb2b3de8be0f892fbf2dc"
  "amdvlk/llvm-project::git+https://github.com/GPUOpen-Drivers/llvm-project#commit=48f1931b2b7afd7e0800fa084b7c0c2176dabfd4"
  "amdvlk/MetroHash::git+https://github.com/GPUOpen-Drivers/MetroHash#commit=18893fb28601bb9af1154cd1a671a121fff6d8d3"
  "amdvlk/CWPack::git+https://github.com/GPUOpen-Drivers/CWPack#commit=4f8cf0584442a91d829d269158567d7ed926f026"
)
sha256sums=('SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP')

build() {

  CFLAGS="${CFLAGS} -flto=thin"
  CXXFLAGS="${CXXFLAGS} -flto=thin"
  LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  sed -i "/<unistd.h>/I a #include <time.h>\n" pal/src/util/lnx/lnxUuid.cpp

  cmake -S xgl -B build \
    -DLLVM_USE_LINKER=lld \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_WAYLAND_SUPPORT=On

  cmake --build build

}

package() {

  install -m755 -d "${pkgdir:?}/usr/lib"
  install -m755 -d "${pkgdir:?}/usr/share/vulkan/icd.d"
  install -m755 -d "${pkgdir:?}/usr/share/vulkan/implicit_layer.d"
  install -m755 -d "${pkgdir:?}/usr/share/licenses"

  install -Dm644 build/icd/amdvlk64.so "${pkgdir:?}/usr/lib/"
  install -Dm644 build/icd/amd_icd64.json "${pkgdir:?}/usr/share/vulkan/icd.d/"
  install -Dm644 build/icd/amd_icd64.json "${pkgdir:?}/usr/share/vulkan/implicit_layer.d/"
  install -Dm644 AMDVLK/LICENSE.txt "${pkgdir:?}/usr/share/licenses/amdvlk"

  sed -i "s#/lib64#/lib#g" "${pkgdir:?}/usr/share/vulkan/icd.d/amd_icd64.json"
  sed -i "s#/lib64#/lib#g" "${pkgdir:?}/usr/share/vulkan/implicit_layer.d/amd_icd64.json"

}
