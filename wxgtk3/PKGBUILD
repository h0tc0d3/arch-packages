pkgname=wxgtk3+clang
pkgver=3.2.0
pkgrel=1
arch=(x86_64)
url='https://wxwidgets.org'
license=(custom:wxWindows)
pkgdesc='GTK+3 implementation of wxWidgets API for GUI'
depends=(zlib gcc-libs expat gtk3 gst-plugins-base-libs glu libsm libnotify)
optdepends=('webkit2gtk: for webview support')
conflicts=('wxgtk<3.0.3.1-2' 'wxgtk-common' 'wxgtk3' 'wxgtk2')
makedepends=(gst-plugins-base webkit2gtk)
options=(!emptydirs)
source=(
  "https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/wxWidgets-${pkgver}.tar.bz2"
)
sha256sums=('356e9b55f1ae3d58ae1fed61478e9b754d46b820913e3bfbc971c50377c1903a')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/wxWidgets-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/wxWidgets-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --with-gtk=3 \
    --with-opengl \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-mediactrl \
    --enable-webview \
    --with-regex=builtin \
    --with-libpng=sys \
    --with-libxpm=sys \
    --with-libjpeg=sys \
    --with-libtiff=sys \
    --disable-precomp-headers

  make
  make -C locale allmo

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "wxWidgets-${pkgver}"
  #mv "${pkgdir:?}/usr/bin/wx-config" "${pkgdir:?}/usr/bin/wx-config-3.1"

  install -D -m644 "wxWidgets-${pkgver}/docs/licence.txt" "${pkgdir:?}/usr/share/licenses/wxgtk3.1"

}
