pkgname=kimageformats+clang
pkgver=5.87.0
pkgrel=1
pkgdesc='Image format plugins for Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
provides=("kimageformats" "kimageformats=${pkgver}")
conflicts=("kimageformats")
depends=(qt5-base)
makedepends=(extra-cmake-modules karchive libavif openexr libheif)
optdepends=('karchive: plugin for Krita and OpenRaster images'
  'libavif: AVIF format support'
  'openexr: EXR format support'
  'libheif: HEIF format support')
groups=(kf5)
source=(
  "https://download.kde.org/stable/frameworks/${pkgver%.*}/kimageformats-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('af6efb6232b8de670691f1983c26e385a64c9f5339d67ae004e97f6864c19830'
  'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S kimageformats-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DKIMAGEFORMATS_HEIF=ON

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
