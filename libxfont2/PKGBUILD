pkgname=libxfont2+clang
pkgver=2.0.6
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
provides=("libxfont2" "libxfont2=${pkgver}")
conflicts=("libxfont2")
depends=('freetype2' 'libfontenc')
makedepends=('xorg-util-macros' 'xtrans' 'xorgproto')
source=("https://xorg.freedesktop.org/archive/individual/lib/libXfont2-${pkgver}.tar.xz"{,.sig})
sha256sums=('74ca20017eb0fb3f56d8d5e60685f560fc85e5ff3d84c61c4cb891e40c27aef4'
  'SKIP')
validpgpkeys=(
  #'C383B778255613DFDB409D91DB221A6900000011' # Keith Packard <keithp@keithp.com>
  #'995ED5C8A6138EB0961F18474C09DD83CAAA50B2' # "Adam Jackson <ajax@nwnk.net>"
  #'C41C985FDCF1E5364576638B687393EE37D128F8' # "Matthieu Herrb <matthieu.herrb@laas.fr>"
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # "Alan Coopersmith <alan.coopersmith@oracle.com>"
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXfont2-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/libXfont2-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-bzip2 \
    --enable-truetype \
    --enable-freetype

  make

}

package() {

  cd "${srcdir:?}/libXfont2-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to libXfont2-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
