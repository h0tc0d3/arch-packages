pkgname=libxtst+clang
pkgver=1.2.4
pkgrel=1
pkgdesc="X11 Testing -- Resource extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
provides=(libxtst "libxtst=${pkgver}")
conflicts=(libxtst)
depends=('libxext' 'libxi' 'libxfixes')
makedepends=('xorg-util-macros' 'xorgproto')
source=("${url}/releases/individual/lib/libXtst-${pkgver}.tar.xz"{,.sig})
sha256sums=('84f5f30b9254b4ffee14b5b0940e2622153b0d3aed8286a3c5b7eeb340ca33c8'
  'SKIP')
validpgpkeys=(
  'C41C985FDCF1E5364576638B687393EE37D128F8' # Matthieu Herrb <matthieu.herrb@laas.fr>
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
)
_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libXtst-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXtst-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

package() {

  cd "${srcdir:?}/libXtst-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libXtst-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
