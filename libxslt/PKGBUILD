pkgbase=libxslt+clang
pkgname=(libxslt+clang libxslt-docs+clang)
pkgver=1.1.37
pkgrel=2
pkgdesc="XML stylesheet transformation library"
url="https://gitlab.gnome.org/GNOME/libxslt/-/wikis/home"
arch=(x86_64)
license=(custom:MIT)
depends=(libgcrypt libxml2 xz)
makedepends=(git python)
checkdepends=(docbook-xml)
_commit=5eca7fb65b7337409a02f9f94fde608edf7d1b0a # tags/v1.1.37^0
source=(
  "git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=${_commit}"
  0001-Make-generate-id-deterministic.patch
)
sha256sums=('SKIP'
  '605e0a0b2ca385a90226bc5b141d3f6bfeade13e43ab39f8dea863505d7cbf18')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Make xsltproc output reproducible for packages which use it to generate documentation
  patch -Np1 -i ../0001-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh

}

pkgver() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/-rc/rc/;s/^v//;s/[^-]*-g/r&/;s/-/+/g'

}

build() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-python=/usr/bin/python \
    --disable-static

  make

}

check() {

  make check -C libxslt

}

package_libxslt+clang() {

  optdepends=('python: Python bindings')
  provides=(lib{,e}xslt.so 'libxslt' "libxslt=${pkgver}")
  conflicts=('libxslt')

  make DESTDIR="${pkgdir:?}" install -C libxslt

  mkdir -p doc/usr/share
  mv "${pkgdir:?}/usr/share/"{doc,gtk-doc} -t doc/usr/share

  install -Dm644 libxslt/Copyright "${pkgdir:?}/usr/share/licenses/libxsl"

}

package_libxslt-docs+clang() {

  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "${pkgdir:?}"

}
