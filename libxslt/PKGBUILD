pkgname=libxslt+clang
pkgver=1.1.36
pkgrel=1
pkgdesc="XML stylesheet transformation library"
url="https://gitlab.gnome.org/GNOME/libxslt/-/wikis/home"
arch=(x86_64)
license=(custom:MIT)
depends=(libgcrypt libxml2 xz)
provides=(lib{,e}xslt.so 'libxslt' "libxslt=${pkgver}")
conflicts=('libxslt')
makedepends=(git python)
checkdepends=(docbook-xml)
optdepends=('python: Python bindings')
_commit=bcd17763a9f371ec5c9bdce0ffa2f2eb3ef74c43 # tags/v1.1.36^0
source=(
  "git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=${_commit}"
  0001-Make-generate-id-deterministic.patch
)
sha256sums=('SKIP'
  'e83c20f9a4f0c95716410c127ad10d6cde814c25d183de0d319f5dda529b7a6e')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Prepare Failed! \E[0m"
    exit 1
  )

  # Make xsltproc output reproducible for packages which use it to generate documentation
  patch -Np1 -i ../0001-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh

}

pkgver() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'

}

build() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

check() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/libxslt" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxslt directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm644 Copyright "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"

}
