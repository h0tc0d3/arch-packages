pkgname=xorg-xwayland+clang
pkgver=22.1.3
pkgrel=1
arch=('x86_64')
license=('custom')
groups=('xorg')
url="https://xorg.freedesktop.org"
pkgdesc="run X clients under wayland"
depends=('nettle' 'libegl' 'libepoxy' 'systemd-libs' 'libxfont2'
  'pixman' 'xorg-server-common' 'libxcvt'
)
makedepends=('meson' 'xorgproto' 'xtrans' 'libxkbfile' 'dbus'
  'xorg-font-util' 'wayland' 'wayland-protocols'
  'libdrm' 'mesa-libgl' 'systemd' 'egl-wayland'
)
source=(
  "https://xorg.freedesktop.org/archive/individual/xserver/xwayland-${pkgver}.tar.xz"{,.sig}
  xwlScaling.diff
)
sha256sums=('a712eb7bce32cd934df36814b5dd046aa670899c16fe98f2afb003578f86a1c5'
  'SKIP'
  '242d5a510286f9363f01220740ae0cbef2844baa53766d1d6257ba570ca07f03')
provides=('xorg-xwayland' "xorg-xwayland=${pkgver}" 'xorg-server-xwayland')
conflicts=('xorg-xwayland' 'xorg-server-xwayland')
replaces=('xorg-server-xwayland')
validpgpkeys=(
  #'B09FAF35BE914521980951145A81AF8E6ADBB200' # "Michel Daenzer <michel@daenzer.net>"
  '67DC86F2623FC5FD4BB5225D14706DBE1E4B4540' # "Olivier Fourdan <fourdan@xfce.org>"
)

_extra_lto_flag=""

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -d "xwayland-${pkgver}" -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  # Add HiDPI support for wayland
  _patch xwlScaling.diff

}

build() {

  LDFLAGS=${LDFLAGS/,-z,now/}

  meson setup xwayland-${pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D ipv6=true \
    -D xvfb=false \
    -D xdmcp=false \
    -D xcsecurity=true \
    -D dri3=true \
    -D xwayland_eglstream=true \
    -D glamor=true \
    -D xkb_dir=/usr/share/X11/xkb \
    -D xkb_output_dir=/var/lib/xkb

  ninja -C build

}

package() {

  # bin + manpage + .pc file
  install -m755 -Dt "${pkgdir:?}/usr/bin" "build/hw/xwayland/Xwayland"
  install -m644 -Dt "${pkgdir:?}/usr/share/man/man1" "build/hw/xwayland/Xwayland.1"
  install -m644 -Dt "${pkgdir:?}/usr/lib/pkgconfig" "build/hw/xwayland/xwayland.pc"

  # license
  install -m644 -Dt "${pkgdir:?}/usr/share/licenses/${pkgname}" "xwayland-${pkgver}/COPYING"

}
