pkgbase=qtcreator+clang
pkgname=(qtcreator+clang qtcreator-devel+clang)
pkgver=7.0.2
_clangver=14.0.6
pkgrel=4
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3)
depends=(qt6-tools qt6-svg qt6-quick3d qt6-webengine qt6-serialport qt6-shadertools qt6-5compat
  "clang=${_clangver}" clazy yaml-cpp) # syntax-highlighting
makedepends=(cmake llvm python)
options=(docs)
source=("https://ftp.fau.de/qtproject/official_releases/qtcreator/${pkgver%.*}/${pkgver}/qt-creator-opensource-src-${pkgver}.tar.xz")
sha256sums=('14dee285aa3aa56fb25ccbd7587382fb6cb05ae99a1a30237f0a0bbef3ceff7a')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -fPIC -flto=thin"
    CXXFLAGS="${CXXFLAGS} -fPIC -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S "qt-creator-opensource-src-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON \
    -DBUILD_QBS=OFF \
    -DQTC_CLANG_BUILDMODE_MATCH=ON

  cmake --build build

  cmake --build build --target docs

}

package_qtcreator+clang() {

  provides=("qtcreator" "qtcreator=${pkgver}")
  conflicts=("qtcreator")

  optdepends=(
    'qt6-doc: integrated Qt documentation'
    'qt6-examples: welcome page examples'
    'qt6-translations: for other languages'
    'gdb: debugger'
    'cmake: cmake project support'
    'x11-ssh-askpass: ssh support'
    'git: git support'
    'mercurial: mercurial support'
    'bzr: bazaar support'
    'valgrind: analyze support'
    'perf: performer analyzer'
    'mlocate: locator filter'
  )

  DESTDIR="${pkgdir:?}" cmake --install build

  # Install docs
  cp -r build/share/doc "${pkgdir:?}/usr/share"

  install -Dm644 "qt-creator-opensource-src-${pkgver}/LICENSE.GPL3-EXCEPT" "${pkgdir:?}/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT"

}

package_qtcreator-devel+clang() {

  provides=("qtcreator-devel" "qtcreator-devel=${pkgver}")
  conflicts=("qtcreator-devel")
  pkgdesc+=' (development files)'
  depends=("qtcreator+clang")

  DESTDIR="${pkgdir:?}" cmake --install build --component Devel

}
