pkgname=plasma-integration+clang
pkgver=5.24.0
pkgrel=1
pkgdesc='Qt Platform Theme integration plugins for the Plasma workspaces'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kio kwayland libxcursor noto-fonts ttf-hack qqc2-desktop-style perl)
provides=("plasma-integration" "plasma-integration=${pkgver}")
conflicts=("plasma-integration")
makedepends=(extra-cmake-modules breeze)
groups=(plasma)
source=(
  "https://download.kde.org/stable/plasma/${pkgver}/plasma-integration-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('3b9f3f3c96d83ffc378e05cf4c351279127d182fccc9333f4478a81fa471b89d'
            'SKIP')
validpgpkeys=(
  'E0A3EB202F8E57528E13E72FD7574483BB57B18D' # Jonathan Riddell <jr@jriddell.org>
  '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D' # Bhushan Shah <bshah@kde.org>
  'D07BD8662C56CB291B316EB2F5675605C74E02CF' # David Edmundson <davidedmundson@kde.org>
  '1FA881591C26B276D7A5518EEAAF29B42A678C20' # Marco Martin <notmart@gmail.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S plasma-integration-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
