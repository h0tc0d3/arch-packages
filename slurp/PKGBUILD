pkgname=slurp+clang
pkgver=1.3.2
pkgrel=3
pkgdesc='Select a region in a Wayland compositor'
arch=('x86_64')
url="https://github.com/emersion/slurp"
license=('MIT')
provides=('slurp' "slurp=${pkgver}")
conflicts=('slurp')
depends=('cairo' 'wayland' 'libxkbcommon')
makedepends=('meson' 'scdoc' 'wayland-protocols')
source=(
  "slurp-${pkgver}.tar.gz::https://github.com/emersion/slurp/releases/download/v${pkgver}/slurp-${pkgver}.tar.gz"
  "slurp-${pkgver}.tar.gz.sig::https://github.com/emersion/slurp/releases/download/v${pkgver}/slurp-${pkgver}.tar.gz.sig"
)
validpgpkeys=("34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48") # emersion
sha256sums=('005dd8601d4bada9728f434728c7a57c955f90f9b9f53d1ffb051abdbabb9876'
  'SKIP')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "slurp-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm 0644 "slurp-${pkgver}/LICENSE" -t "${pkgdir:?}/usr/share/licenses/slurp/"
  install -Dm 0644 "slurp-${pkgver}/README.md" -t "${pkgdir:?}/usr/share/doc/slurp/"

}
