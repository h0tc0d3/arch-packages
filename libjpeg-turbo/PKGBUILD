pkgname=libjpeg-turbo+clang
pkgver=2.1.3
pkgrel=2
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
url='https://libjpeg-turbo.org/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'nasm')
provides=('libjpeg' 'libjpeg-turbo' "libjpeg-turbo=${pkgver}" 'libjpeg.so' 'libturbojpeg.so')
conflicts=('libjpeg-turbo')
source=(
  "https://sourceforge.net/projects/libjpeg-turbo/files/${pkgver}/libjpeg-turbo-${pkgver}.tar.gz"{,.sig}
)
sha256sums=('467b310903832b033fe56cd37720d1b73a6a3bd0171dbf6ff0b620385f4f76d0'
  'SKIP')

validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -S "libjpeg-turbo-${pkgver}" -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_DOCDIR=/usr/share/doc/libjpeg-turbo \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_JPEG8=ON \
    -W no-dev

  cmake --build build

}

check() {

  make test -C build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  install -d "${pkgdir:?}/usr/share/licenses/libjpeg-turbo"
  ln -s /usr/share/doc/libjpeg-turbo/LICENSE.md "${pkgdir:?}/usr/share/licenses/libjpeg-turbo"
  # header required by some dependants
  # https://bugs.archlinux.org/task/24787
  install -m 644 "libjpeg-turbo-${pkgver}/jpegint.h" "${pkgdir:?}/usr/include"

}
