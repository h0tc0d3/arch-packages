pkgname=rz-ghidra+clang
pkgver=0.4.0
pkgrel=3
pkgdesc='Deep ghidra decompiler integration for rizin and rz-cutter'
url='https://github.com/rizinorg/rz-ghidra'
arch=('x86_64')
license=('LGPL3')
provides=(rz-ghidra "rz-ghidra=${pkgver}") \
conflicts=(rz-ghidra)
depends=('rizin' 'glibc')
makedepends=('git' 'cmake' 'qt5-base')
_commit=e70aa0f68310f18620153eed57b27fdfb9ba3018
_cuttercommit=7eb4311c2be2c0e5e6bcd098abf06beda2e96519
_ghidracommit=6e66f058af2eec5be2ebc952c942fcf253e10126
source=(
  "git+https://github.com/rizinorg/rz-ghidra#commit=${_commit:?}"
  "git+https://github.com/rizinorg/cutter#commit=${_cuttercommit:?}"
  "rizin-ghidra::git+https://github.com/rizinorg/ghidra#commit=${_ghidracommit:?}"
  "git+https://github.com/zeux/pugixml.git"
  fix-build.patch
  crc32.h
)
sha256sums=('SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  '0894cf0274605bb60f978f9eb7dd9fbc5be87b04218597fa452a8b1ea7ad297b'
  '2577d973296986e5c5584f74568db475d6c2eebbbf8ed0378bf5dfbae1faa86a')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/rz-ghidra" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/rz-ghidra directory! Prepare Failed! \E[0m"
    exit 1
  )

  rm -fr "third-party/pugixml" "ghidra/ghidra"
  ln -sf "${srcdir:?}/pugixml" "third-party/pugixml"
  ln -sf "${srcdir:?}/rizin-ghidra" "ghidra/ghidra"

  #git submodule init
  #git config submodule."third-party/pugixml".url "${srcdir:?}/pugixml"
  #git config submodule."ghidra/ghidra".url "${srcdir:?}/rizin-ghidra"
  #git submodule update --recursive

  patch -p1 -i "${srcdir:?}/fix-build.patch"

  cp -v "${srcdir:?}/crc32.h" \
    ghidra/ghidra/Ghidra/Features/Decompiler/src/decompile/cpp/crc32.hh

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S rz-ghidra \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_CUTTER_PLUGIN=ON \
    -DBUILD_SLASPECS=ON \
    -DCUTTER_SOURCE_DIR="${srcdir:?}/cutter"

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

  # move shared library into RZ_LIBR_PLUGINS
  rzversion=$(rizin -version | grep rizin | sed -E 's/rizin ([0-9\.]+) .*/\1/')
  #install -d "${pkgdir:?}/usr/lib/rizin/${rzversion}"
  #mv "${pkgdir:?}/usr/lib/rizin/plugins/"*.so "${pkgdir:?}/usr/lib/rizin/${rzversion}"

  install -d "${pkgdir:?}/usr/share/rizin/cutter/plugins/native"
  mv "${pkgdir:?}/build/.local/share/rizin/cutter/plugins/native/librz_ghidra_cutter.so" \
    "${pkgdir:?}/usr/share/rizin/cutter/plugins/native"
  rm -fr "${pkgdir:?}/build"

  ln -s "/usr/lib/rizin/${rzversion}" "${pkgdir:?}/usr/lib/rizin/last"

}
