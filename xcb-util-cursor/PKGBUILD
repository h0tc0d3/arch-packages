pkgname=xcb-util-cursor+clang
pkgver=0.1.4
pkgrel=1
pkgdesc='XCB cursor library'
arch=('x86_64')
url='https://cgit.freedesktop.org/xcb/util-cursor'
license=('custom:MIT')
provides=("xcb-util-cursor" "xcb-util-cursor=${pkgver}")
conflicts=("xcb-util-cursor")
depends=('libxcb' 'xcb-util-renderutil' 'xcb-util-image')
makedepends=('xorgproto')
source=("https://xcb.freedesktop.org/dist/xcb-util-cursor-${pkgver}.tar.xz"{,.sig})
sha256sums=('28dcfe90bcab7b3561abe0dd58eb6832aa9cc77cfe42fcdfa4ebe20d605231fb'
  'SKIP')
validpgpkeys=(
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
)
_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/xcb-util-cursor-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-cursor-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --disable-static

  make

}

package() {

  cd "${srcdir:?}/xcb-util-cursor-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/xcb-util-cursor-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install
  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/LICENSE"

}
