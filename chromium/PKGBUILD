pkgname=chromium+clang
pkgver=102.0.5005.115
pkgrel=1
_launcher_ver=8
_gcc_patchset=5
pkgdesc="A web browser built for speed, simplicity, and security"
arch=('x86_64')
url="https://www.chromium.org/Home"
license=('BSD')
provides=('chromium' "chromium=${pkgver}")
conflicts=('chromium')
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
  'ttf-liberation' 'systemd' 'dbus' 'libpulse' 'pciutils' 'libva'
  'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('python' 'gn' 'ninja' 'clang' 'lld' 'gperf' 'nodejs' 'pipewire'
  'java-runtime-headless' 'git')
optdepends=(
  'pipewire: WebRTC desktop sharing under Wayland'
  'kdialog: support for native dialogs in Plasma'
  'org.freedesktop.secrets: password storage backend on GNOME / Xfce'
  'kwallet: support for storing passwords in KWallet on Plasma'
)
source=(
  "https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
  "https://github.com/foutrelis/chromium-launcher/archive/v${_launcher_ver}/chromium-launcher-${_launcher_ver}.tar.gz"
  "https://github.com/stha09/chromium-patches/releases/download/chromium-${pkgver%%.*}-patchset-${_gcc_patchset}/chromium-${pkgver%%.*}-patchset-${_gcc_patchset}.tar.xz"
  chromium-no-libpng-prefix.patch
  chromium-jpeg-nomangle.patch
  chromium-no-zlib-mangle.patch
  chromium-use-oauth2-client-switches-as-default.patch
  sql-make-VirtualCursor-standard-layout-type.patch
  chromium-EnumTable-crash.patch
  chromium-gcc11.patch
  chromium-InkDropHost-crash.patch
  chromium-libxml-unbundle.patch
  iwyu-add-utility-for-std-exchange.patch
  roll-src-third_party-ffmpeg.patch
  remove-no-opaque-pointers-flag.patch
)
sha256sums=('301d40e5373b6dad9bc6aeb6898116d1f7dd2d0589ed18e108c56e2290df47e6'
  '213e50f48b67feb4441078d50b0fd431df34323be15be97c55302d3fdac4483a'
  '53de0f936fd571e578ba2fbf348c8741116cdcceac3ea6fae5008d8f054a7698'
  '70a2805a63df15817a3435e983c0350c5d04ff62deffd8214cbb040b904f54a5'
  '15c93da644dbc533a6b74bd7f86a07dc59023c7b369298acd649715b9a9209a5'
  'c162eafc1f128e616281e719d860ec8726eec89660f449ed0e9af7dfe07c8cc9'
  'e393174d7695d0bafed69e868c5fbfecf07aa6969f3b64596d0bae8b067e1711'
  'b94b2e88f63cfb7087486508b8139599c89f96d7a4181c61fec4b4e250ca327a'
  'bb4560e067eb6ff6d4c4a5c4f149d1cc1c9c705a14be4721ead2ebb8ec84ded1'
  '5690089b8bd6946dfa975b8022531213b882b3396a3c968eeef9836b131725b7'
  '04bba6fb19ea5a4ab3949b65f06c88728a00ab296f42022ece62ca2fa25ec2e7'
  'fd3bf124aacc45f2d0a4f1dd86303fa7f2a3d4f4eeaf33854631d6cb39e12485'
  '6f666ef0acb08704ca58cc0d5e97e7ce64d8fea51042e593adae1ce15a61231c'
  '30df59a9e2d95dcb720357ec4a83d9be51e59cc5551365da4c0073e68ccdec44'
  '00c16ce83ea4ca924a50fa0cfc2b2a4d744c722f363b065323e6ba0fcbac45a5')
# Possible replacements are listed in build/linux/unbundle/replace_gn_files.py
# Keys are the names in the above script; values are the dependencies in Arch
declare -gA _system_libs=(
  [ffmpeg]=ffmpeg
  [flac]=flac
  [fontconfig]=fontconfig
  [freetype]=freetype2
  ['harfbuzz-ng']=harfbuzz
  [icu]=icu
  [libdrm]=libdrm
  [libjpeg]=libjpeg
  [libpng]=libpng
  #[libvpx]=libvpx
  [libwebp]=libwebp
  [libxml]=libxml2
  [libxslt]=libxslt
  [opus]=opus
  [re2]=re2
  [snappy]=snappy
  [zlib]=minizip
)

_unwanted_bundled_libs=(
  $(printf "%s\n" ${!_system_libs[@]} | sed 's/^libjpeg$/&_turbo/')
)

depends+=(${_system_libs[@]})

# Google API keys (see https://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys.
#
# Starting with Chromium 89 (2021-03-02) the OAuth2 credentials have been left
# out: https://archlinux.org/news/chromium-losing-sync-support-in-early-march/
_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

_rpatch() {

  echo -e "\E[1;33m Revert patch: ${1}\E[0m"
  patch -Rp1 -i "${srcdir:?}/${1}"

}

prepare() {

  cd "${srcdir:?}/chromium-${pkgver}"

  # Allow building against system libraries in official builds
  sed -i 's/OFFICIAL_BUILD/GOOGLE_CHROME_BUILD/' \
    tools/generate_shim_headers/generate_shim_headers.py

  # https://crbug.com/893950
  sed -i -e 's/\<xmlMalloc\>/malloc/' -e 's/\<xmlFree\>/free/' \
    third_party/blink/renderer/core/xml/*.cc \
    third_party/blink/renderer/core/xml/parser/xml_document_parser.cc \
    third_party/libxml/chromium/*.cc \
    third_party/maldoca/src/maldoca/ole/oss_utils.h

  # Use the --oauth2-client-id= and --oauth2-client-secret= switches for
  # setting GOOGLE_DEFAULT_CLIENT_ID and GOOGLE_DEFAULT_CLIENT_SECRET at
  # runtime -- this allows signing into Chromium without baked-in values
  _patch chromium-use-oauth2-client-switches-as-default.patch

  # Do not prefix libpng functions
  _patch chromium-no-libpng-prefix.patch
  # Do not mangle libjpeg
  _patch chromium-jpeg-nomangle.patch
  # Do not mangle zlib
  _patch chromium-no-zlib-mangle.patch

  # https://chromium-review.googlesource.com/c/chromium/src/+/2862724
  _patch sql-make-VirtualCursor-standard-layout-type.patch

  # Upstream fixes
  _patch chromium-EnumTable-crash.patch
  _patch chromium-gcc11.patch
  _patch chromium-InkDropHost-crash.patch
  _patch iwyu-add-utility-for-std-exchange.patch
  _rpatch roll-src-third_party-ffmpeg.patch
  _patch remove-no-opaque-pointers-flag.patch

  # https://chromium-review.googlesource.com/c/chromium/src/+/3488058
  _patch chromium-libxml-unbundle.patch

  _patch patches/chromium-102-fenced_frame_utils-include.patch
  _patch patches/chromium-102-regex_pattern-array.patch

  # Link to system tools required by the build
  mkdir -p third_party/node/linux/node-linux-x64/bin
  ln -sf /usr/bin/node third_party/node/linux/node-linux-x64/bin/
  ln -sf /usr/bin/java third_party/jdk/current/bin/

  # Remove bundled libraries for which we will use the system copies; this
  # *should* do what the remove_bundled_libraries.py script does, with the
  # added benefit of not having to list all the remaining libraries
  local _lib
  for _lib in ${_unwanted_bundled_libs[@]}; do
    find "third_party/${_lib}" -type f \
      \! -path "third_party/${_lib}/chromium/*" \
      \! -path "third_party/${_lib}/google/*" \
      \! -path "third_party/harfbuzz-ng/utils/hb_scoped.h" \
      \! -regex '.*\.\(gn\|gni\|isolate\)' \
      -delete
  done

  ./build/linux/unbundle/replace_gn_files.py \
    --system-libraries "${!_system_libs[@]}"

  rm -rf buildtools/third_party/libc++/BUILD.gn

}

build() {

  ulimit -n 4096

  make -C "chromium-launcher-${_launcher_ver}" "-j$(nproc)"

  cd "${srcdir:?}/chromium-${pkgver}" || exit 1

  local _flags=(
    'custom_toolchain="//build/toolchain/linux/unbundle:default"'
    'host_toolchain="//build/toolchain/linux/unbundle:default"'
    'v8_snapshot_toolchain="//build/toolchain/linux/unbundle:default"'
    'is_clang=true'
    'clang_use_chrome_plugins=false'
    'use_lld=true'
    'is_official_build=true' # implies is_cfi=true on x86_64
    'chrome_pgo_phase=0'
    'is_debug=false'
    'is_asan=false'
    'is_cfi=false'
    'is_hwasan=false'
    'is_lsan=false'
    'is_msan=false'
    'is_tsan=false'
    'is_ubsan=false'
    'is_ubsan_null=false'
    'is_ubsan_no_recover=false'
    'is_ubsan_security=false'
    'is_ubsan_vptr=false'
    'msan_track_origins=0'
    'sanitizer_coverage_flags=""'
    'use_afl=false'
    'use_cfi_diag=false'
    'use_cfi_recover=false'
    'use_libfuzzer=false'
    'use_locally_built_instrumented_libraries=false'
    'use_sanitizer_coverage=false'
    'symbol_level=0'
    'blink_symbol_level=0'
    'blink_enable_generated_code_formatting=false'
    'treat_warnings_as_errors=false'
    'disable_fieldtrial_testing_config=true'
    'ffmpeg_branding="Chrome"'
    'proprietary_codecs=true'
    'rtc_use_pipewire=true'
    'link_pulseaudio=true'
    'use_gnome_keyring=false'
    'use_sysroot=false'
    'use_custom_libcxx=false'
    'use_system_libpng=true'
    'use_system_libjpeg=true'
    'use_system_zlib=true'
    'use_system_harfbuzz=true'
    'use_partition_alloc=true'
    'use_vaapi=true'
    'enable_hangout_services_extension=true'
    'enable_widevine=true'
    'enable_nacl=false'
    "google_api_key=\"${_google_api_key}\""
  )

  if [[ "${CFLAGS=}" =~ znver2 ]]; then
    _flags+=('x64_arch="znver2"')
  else
    _flags+=('x64_arch="x86-64-v3"')
  fi

  if [[ -n ${_system_libs[icu]+set} ]]; then
    _flags+=('icu_use_data_file=false')
  fi

  CFLAGS=${CFLAGS/--param ssp-buffer-size=4 -fstack-clash-protection/}     # Affects stability
  CXXFLAGS=${CXXFLAGS/--param ssp-buffer-size=4 -fstack-clash-protection/} # Affects stability
  # Facilitate deterministic builds (taken from build/config/compiler/BUILD.gn)
  CFLAGS+=' -Wno-builtin-macro-redefined'
  CXXFLAGS+=' -Wno-builtin-macro-redefined'
  CPPFLAGS+=' -D__DATE__=  -D__TIME__=  -D__TIMESTAMP__='

  # Do not warn about unknown warning options
  CFLAGS+=' -Wno-unknown-warning-option'
  CXXFLAGS+=' -Wno-unknown-warning-option'

  CFLAGS=${CFLAGS/-fexceptions/}
  CFLAGS=${CFLAGS/-fcf-protection/}
  CXXFLAGS=${CXXFLAGS/-fexceptions/}
  CXXFLAGS=${CXXFLAGS/-fcf-protection/}

  # This appears to cause random segfaults
  CFLAGS=${CFLAGS/-fstack-clash-protection/}
  CXXFLAGS=${CXXFLAGS/-fstack-clash-protection/}

  # https://crbug.com/957519#c122
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/}

  gn gen out/Release --args="${_flags[*]}"
  ninja -C out/Release chrome chrome_sandbox chromedriver

}

package() {

  cd "chromium-launcher-${_launcher_ver}" || exit 1

  make PREFIX=/usr DESTDIR="${pkgdir:?}" install
  install -Dm644 LICENSE \
    "${pkgdir:?}/usr/share/licenses/chromium/LICENSE.launcher"

  cd "${srcdir:?}/chromium-${pkgver}" || exit 1

  install -D out/Release/chrome "${pkgdir:?}/usr/lib/chromium/chromium"
  install -Dm4755 out/Release/chrome_sandbox "${pkgdir:?}/usr/lib/chromium/chrome-sandbox"
  ln -sf /usr/lib/chromium/chromedriver "${pkgdir:?}/usr/bin/chromedriver"

  install -Dm644 chrome/installer/linux/common/desktop.template \
    "${pkgdir:?}/usr/share/applications/chromium.desktop"
  install -Dm644 chrome/app/resources/manpage.1.in \
    "${pkgdir:?}/usr/share/man/man1/chromium.1"
  sed -i \
    -e 's/@@MENUNAME@@/Chromium/g' \
    -e 's/@@PACKAGE@@/chromium/g' \
    -e 's/@@USR_BIN_SYMLINK_NAME@@/chromium/g' \
    "${pkgdir:?}/usr/share/applications/chromium.desktop" \
    "${pkgdir:?}/usr/share/man/man1/chromium.1"

  install -Dm644 chrome/installer/linux/common/chromium-browser/chromium-browser.appdata.xml \
    "${pkgdir:?}/usr/share/metainfo/chromium.appdata.xml"
  sed -ni \
    -e 's/chromium-browser\.desktop/chromium.desktop/' \
    -e '/<update_contact>/d' \
    -e '/<p>/N;/<p>\n.*\(We invite\|Chromium supports Vorbis\)/,/<\/p>/d' \
    -e '/^<?xml/,$p' \
    "${pkgdir:?}/usr/share/metainfo/chromium.appdata.xml"

  local toplevel_files=(
    chrome_100_percent.pak
    chrome_200_percent.pak
    resources.pak
    chromedriver
    chrome_crashpad_handler
    v8_context_snapshot.bin
    # ANGLE
    libEGL.so
    libGLESv2.so
    # SwiftShader ICD
    libvk_swiftshader.so
    vk_swiftshader_icd.json
  )

  if [[ -z ${_system_libs[icu]+set} ]]; then
    toplevel_files+=(icudtl.dat)
  fi

  cp "${toplevel_files[@]/#/out/Release/}" "${pkgdir:?}/usr/lib/chromium/"
  install -Dm644 -t "${pkgdir:?}/usr/lib/chromium/locales" out/Release/locales/*.pak

  for size in 24 48 64 128 256; do
    install -Dm644 "chrome/app/theme/chromium/product_logo_$size.png" \
      "${pkgdir:?}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
  done

  for size in 16 32; do
    install -Dm644 "chrome/app/theme/default_100_percent/chromium/product_logo_$size.png" \
      "${pkgdir:?}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
  done

  install -Dm644 LICENSE "${pkgdir:?}/usr/share/licenses/chromium/LICENSE"

}
