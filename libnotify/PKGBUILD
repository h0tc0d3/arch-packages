pkgname=libnotify+clang
pkgver=0.8.1
pkgrel=1
pkgdesc="Library for sending desktop notifications"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/libnotify"
license=(LGPL)
provides=(libnotify.so 'libnotify' "libnotify=${pkgver}")
conflicts=('libnotify')
depends=(gdk-pixbuf2 libg{lib,object,io}-2.0.so libgdk_pixbuf-2.0.so)
makedepends=(gtk3 gobject-introspection git meson docbook-xsl xmlto gtk-doc)
_commit=650f2f123e75469b85d81fbca66e17b744a7714b # tags/0.8.1^0
source=("git+https://gitlab.gnome.org/GNOME/libnotify.git#commit=${_commit:?}")
sha256sums=('SKIP')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "libnotify" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  meson install -C build --destdir "${pkgdir:?}"

}
