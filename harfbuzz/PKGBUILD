pkgbase=harfbuzz+clang
pkgname=(harfbuzz+clang harfbuzz-icu+clang)
pkgver=4.2.0
pkgrel=3
pkgdesc="OpenType text shaping engine"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu gobject-introspection gtk-doc
  ragel git python meson chafa)
checkdepends=(python-fonttools python-setuptools)
_commit=9d5730b958974bc9db95e46e6bad52e9e9cd6e1c # tags/4.2.0^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=${_commit}")
sha256sums=('SKIP')

_extra_lto_flag=""

pkgver() {

  cd "${srcdir:?}/harfbuzz" || (
    echo -e "\E[1;31mCan't cd to {srcdir:?}/harfbuzz directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup harfbuzz build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode=nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D gobject=enabled \
    -D graphite2=enabled \
    -D introspection=enabled

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package_harfbuzz+clang() {
  depends=('glib2' 'freetype2' 'graphite' 'libglib-2.0.so' 'libfreetype.so'
    'libgobject-2.0.so')
  provides=('harfbuzz' "harfbuzz=${pkgver}" 'libharfbuzz.so' 'libharfbuzz-subset.so' 'libharfbuzz-gobject.so')
  conflicts=('harfbuzz')
  optdepends=('cairo: hb-view program'
    'chafa: hb-view program')

  meson install -C build --destdir "${pkgdir:?}"

  # Split harfbuzz-icu
  mkdir -p hb-icu/usr/{include/harfbuzz,lib/pkgconfig}
  mv -t hb-icu/usr/lib "${pkgdir:?}/usr/lib/libharfbuzz-icu"*
  mv -t hb-icu/usr/lib/pkgconfig "${pkgdir:?}/usr/lib/pkgconfig/harfbuzz-icu.pc"
  mv -t hb-icu/usr/include/harfbuzz "${pkgdir:?}/usr/include/harfbuzz/hb-icu.h"

  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname:?}" -m644 harfbuzz/COPYING

}

package_harfbuzz-icu+clang() {

  pkgdesc="${pkgdesc} (ICU integration)"
  depends=("harfbuzz+clang=${pkgver}-${pkgrel}" 'libicuuc.so' 'libharfbuzz.so')
  provides=('harfbuzz-icu' "harfbuzz-icu=${pkgver}" 'libharfbuzz-icu.so')
  conflicts=('harfbuzz-icu')

  mv hb-icu/* "${pkgdir:?}"

  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname:?}" -m644 harfbuzz/COPYING

}
