pkgname=vde2+clang
pkgver=2.3.3
pkgrel=2
pkgdesc="Virtual Distributed Ethernet for emulators like qemu"
arch=(x86_64)
url="https://github.com/virtualsquare/vde-2"
license=(GPL2 LGPL2.1 BSD)
provides=(vde2 "vde2=${pkgver}" libvdehist.so libvdemgmt.so libvdeplug.so libvdesnmp.so)
conflicts=(vde2)
depends=(bash libpcap.so openssl python)
source=(
  "vde2-${pkgver}.tar.gz::https://github.com/virtualsquare/vde-2/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=('a7d2cc4c3d0c0ffe6aff7eb0029212f2b098313029126dcd12dc542723972379')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/vde-2-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/vde-2-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

  autoreconf -fiv

}

build() {

  cd "${srcdir:?}/vde-2-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/vde-2-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/vde2 \
    --enable-experimental

  # NOTE: build can not be parallelized
  make V=1

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "vde-2-${pkgver}"

  # install slirpvde license
  install -Dm 644 "vde-2-${pkgver}/COPYING"* -t "${pkgdir:?}/usr/share/licenses/vde2/"

}
