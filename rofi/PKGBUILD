pkgname=rofi+clang
pkgver=1.7.5
pkgrel=1
pkgdesc='A window switcher, application launcher and dmenu replacement. X11 and Wayland support.'
arch=(x86_64)
url="https://github.com/lbonn/rofi"
license=(MIT)
depends=(libxdg-basedir startup-notification libxkbcommon-x11 xcb-util-wm xcb-util-xrm librsvg)
makedepends=('git' 'wayland' 'wayland-protocols' 'meson')
provides=('rofi' "rofi=${pkgver}")
optdepends=('i3-wm: use as a window switcher' 'sway: use as a window switcher')
_commit=9ec86220d55a72e89a60f357a71d3572f130f885
source=("rofi::git+https://github.com/lbonn/rofi.git#commit=${_commit}")
sha256sums=('SKIP')

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "rofi" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D wayland=enabled \
    -D xcb=enabled \
    -D drun=true \
    -D window=true \
    -D check=disabled

  meson compile -C build

}

check() {

  meson test -C build --print-errorlogs

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  install -Dm644 "${srcdir:?}/rofi/COPYING" "${pkgdir:?}/usr/share/licenses/rofi/COPYING"
  install -dm755 "${srcdir:?}/rofi/examples" "${pkgdir:?}/usr/share/doc/rofi/examples"
  install -Dm755 "${srcdir:?}/rofi/Examples/"*.sh "${pkgdir:?}/usr/share/doc/rofi/examples"

}
