pkgname=libdrm+clang
pkgver=2.4.112
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
provides=('libdrm' "libdrm=${pkgver}")
conflicts=('libdrm')
depends=('libpciaccess')
makedepends=('valgrind' 'python-docutils' 'meson')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
source=(
  "https://dri.freedesktop.org/libdrm/libdrm-${pkgver}.tar.xz"
  COPYING
  libdrm-2.4.0-no-bc.patch
)
sha256sums=('00b07710bd09b35cd8d80eaf4f4497fe27f4becf467a9830f1f5e8324f8420ff'
  '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93'
  '86aef9a371fab4dcb758b05baf9f797b622f443b1f63e3cf2205353de4a09341')
validpgpkeys=(
  '10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE' # David Airlie <airlied@redhat.com>
  #'34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48' # emersion <contact@emersion.fr>
  #'E9E246A89E65374EB8355796B8B5FA0EFC1EC2FE' # "Leo Liu <leo.liu@amd.com>"
  #'899A810900B815807742E6D895A677A230AC4AA9' # Bas Nieuwenhuizen
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

  patch -d "libdrm-${pkgver}" -p1 -i "${srcdir:?}/libdrm-2.4.0-no-bc.patch"

}

build() {

  meson setup "libdrm-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D default_library=both \
    -D radeon=true \
    -D amdgpu=true \
    -D nouveau=true \
    -D intel=true \
    -D udev=true \
    -D valgrind=false

  meson compile -C build

}

check() {

  meson test -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build
  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname}" -m644 COPYING

}
