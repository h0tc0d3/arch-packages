pkgname=libdrm+clang
pkgver=2.4.113
pkgrel=3
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
provides=('libdrm' "libdrm=${pkgver}")
conflicts=('libdrm')
depends=('libpciaccess')
makedepends=('python-docutils' 'meson')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
source=(
  "https://dri.freedesktop.org/libdrm/libdrm-${pkgver}.tar.xz"
  COPYING
)
sha256sums=('7fd7eb2967f63beb4606f22d50e277d993480d05ef75dd88a9bd8e677323e5e1'
  '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')
validpgpkeys=(
  '10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE' # David Airlie <airlied@redhat.com>
  #'34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48' # emersion <contact@emersion.fr>
  #'E9E246A89E65374EB8355796B8B5FA0EFC1EC2FE' # "Leo Liu <leo.liu@amd.com>"
  #'899A810900B815807742E6D895A677A230AC4AA9' # Bas Nieuwenhuizen
)

_extra_lto_flag=""

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _extra_lto_flag='-D b_lto_mode=thin'
  fi

}

build() {

  meson setup "libdrm-${pkgver}" build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D default_library=both \
    -D radeon=enabled \
    -D amdgpu=enabled \
    -D nouveau=enabled \
    -D intel=enabled \
    -D udev=true \
    -D valgrind=disabled

  meson compile -C build

}

check() {

  meson test -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build
  install -Dt "${pkgdir:?}/usr/share/licenses/${pkgname}" -m644 COPYING

}
