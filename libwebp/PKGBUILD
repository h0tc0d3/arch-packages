pkgname=libwebp+clang
pkgver=1.2.4
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=(x86_64)
url="https://developers.google.com/speed/webp/"
license=(BSD)
provides=("libwebp" "libwebp=${pkgver}" libwebp{,decoder,demux,mux}.so)
conflicts=("libwebp")
depends=(libpng libjpeg libtiff giflib)
makedepends=(freeglut mesa glu git)
optdepends=('freeglut: vwebp viewer')
source=("https://storage.googleapis.com/downloads.webmproject.org/releases/webp/libwebp-${pkgver}.tar.gz"{,.asc})
sha256sums=('7bf5a8a28cc69bcfa8cb214f2c3095703c6b73ac5fba4d5480c205331d9494df'
  'SKIP')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D') # WebP release signing key

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libwebp-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! Prepare Failed! \E[0m"
    exit 1
  )

  NOCONFIGURE=1 ./autogen.sh

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/libwebp-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-swap-16bit-csp \
    --enable-libwebp{mux,demux,decoder,extras}

  make

}

check() {

  make check -C "libwebp-${pkgver}"

}

package() {

  make DESTDIR="${pkgdir:?}" install -C "libwebp-${pkgver}"

  install -Dm644 "libwebp-${pkgver}/COPYING" "${pkgdir:?}/usr/share/licenses/${pkgname}/COPYING"

}
