pkgname=libwebp+clang
pkgver=1.2.1
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=(x86_64)
url="https://developers.google.com/speed/webp/"
license=(BSD)
provides=("libwebp" "libwebp=${pkgver}")
conflicts=("libwebp")
depends=(libpng libjpeg libtiff giflib)
makedepends=(freeglut mesa glu git)
optdepends=('freeglut: vwebp viewer')
_commit=9ce5843dbabcfd3f7c39ec7ceba9cbeb213cbfdf # tags/v1.2.1^0
source=("git+https://chromium.googlesource.com/webm/libwebp#commit=${_commit}")
sha256sums=('SKIP')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D') # WebP release signing key

_llvm=0

pkgver() {

  cd "${srcdir:?}/libwebp" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! PkgVer Failed! \E[0m"
    exit 1
  )

  git describe --tags | sed 's/^v//;s/-/+/g'

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  cd "${srcdir:?}/libwebp" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! Prepare Failed! \E[0m"
    exit 1
  )

  NOCONFIGURE=1 ./autogen.sh

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/libwebp" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-swap-16bit-csp \
    --enable-experimental \
    --enable-libwebp{mux,demux,decoder,extras}

  make

}

check() {

  cd "${srcdir:?}/libwebp" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! Check Failed! \E[0m"
    exit 1
  )

  make check

}

package() {

  cd "${srcdir:?}/libwebp" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libwebp directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}" install

  install -Dm644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/COPYING"

}
