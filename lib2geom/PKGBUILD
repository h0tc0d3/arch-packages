pkgname=lib2geom+clang
pkgver=1.2
pkgrel=1
pkgdesc='Easy to use 2D geometry library in C++'
arch=('x86_64')
provides=("lib2geom" "lib2geom=${pkgver}")
conflicts=("lib2geom")
url='https://gitlab.com/inkscape/lib2geom'
license=('GPL')
depends=('glib2' 'gsl' 'double-conversion' 'cairo' 'ragel' 'gtest')
makedepends=('cmake' 'boost')
source=("https://gitlab.com/inkscape/lib2geom/-/archive/${pkgver}/lib2geom-${pkgver}.tar.bz2")
sha256sums=('c32e384831b62af33c42eca65fa205b223c5da14e07602dc005ccf7e1c3cb1b1')

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cmake -B build -S lib2geom-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -D2GEOM_BOOST_PYTHON=NO \
    -D2GEOM_BUILD_SHARED=YES \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build

}

package() {

  DESTDIR="${pkgdir:?}" cmake --install build

}
