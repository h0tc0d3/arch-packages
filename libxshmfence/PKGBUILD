pkgname=libxshmfence+clang
pkgver=1.3.1
pkgrel=1
pkgdesc="a library that exposes a event API on top of Linux futexes"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('GPL')
provides=(libxshmfence "libxshmfence=${pkgver}")
conflicts=(libxshmfence)
depends=('glibc')
makedepends=('xorg-util-macros' 'xorgproto')
source=("${url}/releases/individual/lib/libxshmfence-${pkgver}.tar.xz"{,.sig})
sha256sums=('1129f95147f7bfe6052988a087f1b7cb7122283d2c47a7dbf7135ce0df69b4f8'
  'SKIP')
validpgpkeys=(
  #'C383B778255613DFDB409D91DB221A6900000011' # Keith Packard <keithp@keithp.com>
  #'3BB639E56F861FA2E86505690FDD682D974CA72A' # "Matt Turner <mattst88@gmail.com>"
  '4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # Alan Coopersmith <alan.coopersmith@oracle.com>
)

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  cd "${srcdir:?}/libxshmfence-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxshmfence-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  # fix some libtools errors
  autoreconf -vfi

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  ./configure \
    --prefix=/usr

  make

}

check() {

  cd "${srcdir:?}/libxshmfence-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxshmfence-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  make -k check

}

package() {

  cd "${srcdir:?}/libxshmfence-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libxshmfence-${pkgver} directory! Package Failed! \E[0m"
    exit 1
  )

  make DESTDIR="${pkgdir:?}/" install

  install -m755 -d "${pkgdir:?}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir:?}/usr/share/licenses/${pkgname}/"

}
