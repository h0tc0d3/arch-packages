pkgname=qt5-translations+clang
pkgver=5.15.5+kde+r2
pkgrel=1
arch=('any')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework (Translations)'
provides=("qt5-translations" "qt5-translations=${pkgver}")
conflicts=("qt5-translations")
makedepends=('git' 'qt5-tools')
groups=('qt' 'qt5')
_commit=0e92fc4f19928387c341533ef3d259efaa752c0f
source=("git+https://invent.kde.org/qt/qt/qttranslations#commit=${_commit}")
sha256sums=('SKIP')

_llvm=0

pkgver() {

  cd "${srcdir:?}/qttranslations" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/qttranslations directory! PkgVer Failed! \E[0m"
    exit 1
  )

  echo "5.15.5+kde+r$(git rev-list --count v5.15.5-lts-lgpl..${_commit})"

}

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

  if [[ ! -d build ]]; then
    mkdir -p build
  fi

}

build() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Build Failed! \E[0m"
    exit 1
  )

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  qmake ../qttranslations
  make

}

package() {

  cd "${srcdir:?}/build" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/build directory! Package Failed! \E[0m"
    exit 1
  )

  make INSTALL_ROOT="${pkgdir:?}" install

  install -d "${pkgdir:?}/usr/share/licenses"
  ln -s /usr/share/licenses/qt5-base "${pkgdir:?}/usr/share/licenses/${pkgname}"

}
