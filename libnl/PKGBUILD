pkgname=libnl+clang
pkgver=3.7.0
pkgrel=1
pkgdesc='Library for applications dealing with netlink sockets'
arch=(x86_64)
url='https://github.com/thom311/libnl/'
license=(GPL)
provides=('libnl' "libnl=${pkgver}")
conflicts=('libnl')
depends=(glibc)
backup=('etc/libnl/classid' 'etc/libnl/pktloc')
source=("https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/libnl-${pkgver}.tar.gz"{,.sig})
sha256sums=('9fe43ccbeeea72c653bdcf8c93332583135cda46a79507bfd0a483bb57f65939'
  'SKIP')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller

_llvm=0

prepare() {

  if clang --version 2>/dev/null | grep -iq "clang\s*version\s*[0-9]" && ld.lld --version 2>/dev/null | grep -iq "LLD\s*[0-9]"; then
    _llvm=1
  fi

}

build() {

  if [[ ${_llvm} -eq 1 ]]; then

    CFLAGS="${CFLAGS} -flto=thin"
    CXXFLAGS="${CXXFLAGS} -flto=thin"
    LDFLAGS="${LDFLAGS} -fuse-ld=lld"

  fi

  cd "${srcdir:?}/libnl-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/libnl-${pkgver} directory! Build Failed! \E[0m"
    exit 1
  )

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static

  make

}

check() {

  make -C libnl-${pkgver} check

}

package() {

  make -C libnl-${pkgver} DESTDIR="${pkgdir:?}" install

}
